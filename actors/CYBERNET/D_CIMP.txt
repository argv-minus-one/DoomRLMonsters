
// CYBER-IMP
//
ACTOR RLCyberneticImp : DoomImp 16538
{
  Health 90
  DamageFactor "Fire", 0.75
  DamageFactor "Crush", 10
  MeleeRange 48
  +DONTHARMSPECIES
  Species "RLImp"
  SeeSound "cyberimp/sight"
  PainSound "cyberimp/pain"
  DeathSound "cyberimp/death"
  ActiveSound "cyberimp/active"
  CrushPainSound "rlmonsters/crush"
  Obituary "The Cyber-Imp showed %o his new arm."
  HitObituary "%o was zapped by a Cyber-Imp."
  Tag "Cyber-Imp"
  States
  {
  Spawn:
    CIMP AB 10 A_Look
    Loop
  See:
    CIMP AABBCCDD 3 A_Chase
    Loop
  Melee:
    CIMP EF 8 A_FaceTarget
  MeleeZappin:
    CIMP G 1 Bright A_CustomMeleeAttack (1 * random(1, 3), "techvile/smallzap", "", "Melee")
    CIMP F 1
    TNT1 A 0 A_Chase("MeleeZappin", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    Goto See
  Missile:
    CIMP EF 8 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    CIMP G 6 Bright A_Custommissile("RLCyberImpPlasmaBall",36,4)
    Goto See
  SummonMissile:
    CIMP G 6 Bright A_Custommissile("RLSummonCyberImpPlasmaBall",36,4)
    Goto See
  Pain:
    CIMP H 2
    CIMP H 2 A_Pain
    Goto See
  Pain.CyberDisruptor:
    CIMP H 2
    CIMP H 6 A_Pain
    CIMP H 6
    CIMP H 6 A_Pain
    CIMP H 6
    CIMP H 6 A_Pain
    CIMP H 6
    CIMP H 6 A_Pain
    Goto See
  Death:
    CIMP I 8
    CIMP J 8 A_Scream
    CIMP K 6
    CIMP L 6 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    CIMP M -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    CIMP N 5 A_CustomMissile("RLCyberImpArm",32,-10,270)
    CIMP O 5 A_XScream
    CIMP P 5
    CIMP Q 5 A_NoBlocking
    CIMP R 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    CIMP ST 5
    CIMP U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    CIMP M 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    CIMP MLKJI 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    CIMP M -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLCyberImpPlasmaBall : PlasmaBall
{
   +THRUGHOST
   Decal "PlasmaScorch"
   DamageType "Plasma"
   Damage 4
   Speed 18
}

ACTOR RLSummonCyberImpPlasmaBall : RLCyberImpPlasmaBall
{
   +MTHRUSPECIES
}

ACTOR RLCyberImpArm
{
	Radius 8
	Height 8
	Speed 4
	+DOOMBOUNCE
	+DROPOFF
    +NOTONAUTOMAP
	+Missile
      States
	{
	Spawn:
     CARM ABCDE 3
     Loop
	Death:
	 CARM F -1
	 Loop
	}
} 

ACTOR RLCyberneticImpReborn : RLCyberneticImp
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    CIMP MLKJI 8
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    CIMP AB 10 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedCyberneticImp : RLCyberneticImp
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCCyberneticImp : RLCyberneticImp
{
  +TOUCHY
  States
  {
  Death:
    CIMP I 8
    CIMP J 8 A_Scream
    CIMP K 6
    CIMP L 6 A_NoBlocking
    CIMP M -1
    Stop
  }
}
