
// FORMER CYBORG
//
ACTOR RLFormerCyborgBattleRifle : ZombieMan 16535
{
  Health 80
  GibHealth -40
  Speed 5
  PainChance 160
  PainThreshold 8
  DamageFactor "Crush", 10
  DamageFactor "Bullet", 0.85
  SeeSound "cyberformer/sight"
  PainSound "cyberformer/pain"
  DeathSound "cyberformer/death"
  ActiveSound "cyberformer/active"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o couldn't topple a Former Cyborg."
  DropItem "RLClip"
  DropItem "RLBattleRiflePickup"
  +AVOIDMELEE
  Tag "Former Cyborg"
  States
  {
  Spawn:
    CRIF AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerCyborgBattleRifleShotCounter", 20, "Reload")
    CRIF AA 4 A_Chase
    TNT1 A 0 A_PlaySound("cyberformer/step", 5)
    CRIF BB 4 A_Chase
    CRIF CC 4 A_Chase
    TNT1 A 0 A_PlaySound("cyberformer/step", 5)
    CRIF DD 4 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory ("RLFormerCyborgBattleRifleShotCounter", 20, "Reload")
    CRIF E 12 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLFormerCyborgBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    CRIF F 2 Bright A_CustomBulletAttack (25, 5, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    CRIF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCyborgBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCyborgBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    CRIF F 2 Bright A_CustomBulletAttack (25, 5, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    CRIF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCyborgBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCyborgBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    CRIF F 2 Bright A_CustomBulletAttack (25, 5, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    CRIF E 2 A_FaceTarget
	Goto MissileEnd
  SummonMissile:
    CRIF F 2 Bright A_CustomBulletAttack (2, 0.5, 1, random(1,3) * 5, "RLSummonEnemyBulletPuff", 0, CBAF_NORANDOM)
    CRIF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCyborgBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCyborgBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    CRIF F 2 Bright A_CustomBulletAttack (2, 0.5, 1, random(1,3) * 5, "RLSummonEnemyBulletPuff", 0, CBAF_NORANDOM)
    CRIF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCyborgBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCyborgBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    CRIF F 2 Bright A_CustomBulletAttack (2, 0.5, 1, random(1,3) * 5, "RLSummonEnemyBulletPuff", 0, CBAF_NORANDOM)
    CRIF E 2 A_FaceTarget
	Goto MissileEnd
  MissileEnd:
    CRIF E 8 A_FaceTarget
    Goto See
  Reload:
    CRIF EE 4
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifleunload", 7)
    CRIF EEEEEEEE 4
    CRIF E 2
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifleload", 7)
    TNT1 A 0 A_TakeInventory ("RLFormerCyborgBattleRifleShotCounter", 20)
    CRIF EE 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/battleriflecock", 5)
    CRIF E 6 A_FaceTarget
    Goto See
  Pain:
    CRIF G 3
    CRIF G 3 A_Pain
    Goto See
  Death:
    CRIF H 5
    CRIF I 5 A_Scream
    CRIF J 5 A_NoBlocking
    CRIF K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    CRIF LM 5
    CRIF N -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    CRIF M 4
    CRIF N 4 A_XScream
    CRIF O 4 A_NoBlocking
    CRIF PQRST 4
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    CRIF UV 4
    CRIF W -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    CRIF L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerCyborgBattleRifleShotCounter", 20)
    CRIF K 5
    CRIF J 5
    CRIF I 5
    CRIF H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    CRIF N -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLFormerCyborgBattleRifleShotCounter : Inventory
{
inventory.maxamount 20
}

ACTOR RLDRPGSummonedFormerCyborg : RLFormerCyborgBattleRifle
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCFormerCyborgBattleRifle : RLFormerCyborgBattleRifle
{
  +TOUCHY
  States
  {
  See:
    CRIF AABBCCDD 4
    Loop
  Missile:
    CRIF E 12
    CRIF F 2 Bright
    CRIF E 2
    CRIF F 2 Bright
    CRIF E 2
    CRIF F 2 Bright
    CRIF E 10
    Goto See
  Death:
    CRIF H 5
    CRIF I 5 A_Scream
    CRIF J 5 A_NoBlocking
    CRIF KLM 5
    CRIF N -1
    Stop
  }
}
