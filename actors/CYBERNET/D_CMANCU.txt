
// VOLACUBUS
//
ACTOR RLCyberneticMancubus : Fatso 16548
{
  Radius 44
  Height 60
  GibHealth -60
  DamageFactor "Crush", 10
  Speed 16
  +DONTHARMSPECIES
  +NOGRAVITY
  +FLOAT
  +FLOATBOB
  +MISSILEMORE
  +DONTCORPSE
  -FLOORCLIP
  Species "RLMancubus"
  DeathSound "volacubus/death1"
  CrushPainSound "rlmonsters/crush"
  Obituary "%o was not expecting a visit from a Volacubus."
  Tag "Volacubus"
  States
  {
  Spawn:
    TNT1 A 0 A_PlaySound("volacubus/jetloop", 7, 1)
    CMAN ABABCDCD 4 A_Look
    Loop
  See:
    TNT1 A 0 A_PlaySound("volacubus/jetloop", 7, 1)
    CMAN ABABCDCD 4 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_PlaySound("volacubus/jetloop", 7, 1)
    TNT1 A 0 A_PlaySound("fatso/raiseguns", CHAN_VOICE)
    CMAN F 20 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    TNT1 A 0 A_CustomMissile("RLVolacbusPlasmaBall", 34, 32, random(-7,4))
    CMAN H 5 Bright A_CustomMissile("RLVolacbusPlasmaBall", 34, -32, random(-4,7))
    CMAN I 5
    CMAN FG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLVolacbusPlasmaBall", 34, 32, random(-7,4))
    CMAN H 5 Bright A_CustomMissile("RLVolacbusPlasmaBall", 34, -32, random(-4,7))
    CMAN I 5
    CMAN FG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLVolacbusPlasmaBall", 34, 32, random(-7,4))
    CMAN H 5 Bright A_CustomMissile("RLVolacbusPlasmaBall", 34, -32, random(-4,7))
    CMAN I 5
    CMAN FG 5
    TNT1 A 0 A_MonsterRefire(128, "See")
    TNT1 A 0 A_Jump(64, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  SummonMissile:
    TNT1 A 0 A_CustomMissile("RLSummonVolacbusPlasmaBall", 34, 32, random(-7,4))
    CMAN H 5 Bright A_CustomMissile("RLSummonVolacbusPlasmaBall", 34, -32, random(-4,7))
    CMAN I 5
    CMAN FG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLSummonVolacbusPlasmaBall", 34, 32, random(-7,4))
    CMAN H 5 Bright A_CustomMissile("RLSummonVolacbusPlasmaBall", 34, -32, random(-4,7))
    CMAN I 5
    CMAN FG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLSummonVolacbusPlasmaBall", 34, 32, random(-7,4))
    CMAN H 5 Bright A_CustomMissile("RLSummonVolacbusPlasmaBall", 34, -32, random(-4,7))
    CMAN I 5
    CMAN FG 5
    TNT1 A 0 A_MonsterRefire(128, "See")
    TNT1 A 0 A_Jump(64, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  Pain:
    CMAN J 3
    CMAN J 3 A_Pain
    Goto See
  Pain.CyberDisruptor:
    CMAN J 2
    CMAN J 6 A_Pain
    CMAN J 6
    CMAN J 6 A_Pain
    CMAN J 6
    CMAN J 6 A_Pain
    CMAN J 6
    CMAN J 6 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_ChangeFlag(FLOATBOB, 0)
    CMAN K 6
    CMAN L 6 A_Scream
    CMAN M 6 A_NoBlocking
    TNT1 A 0 A_PlaySound("volacubus/jetoff", 7)
    CMAN N 6
    TNT1 A 0 A_ChangeFlag ("CORPSE", 1)
    TNT1 A 0 A_ChangeFlag ("DONTCORPSE", 0)
  DeathFall:
    CMAN N 1
	Loop
  Crash:
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    TNT1 A 0 A_PlaySound("volacubus/death2", CHAN_VOICE)
    CMAN OPQRS 6
    TNT1 A 0 A_SetFloorClip
    CMAN T -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    CMAN T 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_UnSetFloorClip
    TNT1 A 0 A_ChangeFlag ("CORPSE", 0)
    TNT1 A 0 A_ChangeFlag ("DONTCORPSE", 1)
    TNT1 A 0 A_PlaySound("volacubus/jeton", 7)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    CMAN RQPONMLK 5
    TNT1 A 0 A_ChangeFlag(FLOATBOB, 1)
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 0 A_ChangeFlag ("CORPSE", 1)
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    CMAN T -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLVolacbusPlasmaBall : BFGBall
{
  Radius 13
  Height 8
  Speed 25
  Damage 10
  DamageType "Plasma"
  Projectile
  +RANDOMIZE
  +THRUGHOST
  RenderStyle Add
  Alpha 0.85
  Decal "EnemyBlueBFGLightning"
  SeeSound "volacubus/fire"
  DeathSound "weapons/bfgx"
  States
  {
  Spawn:
    WPLZ AB 4 Bright
    Loop
  Death:
    WPLZ CDE 4 Bright
    Stop
  }
}

ACTOR RLSummonVolacbusPlasmaBall : RLVolacbusPlasmaBall
{
  +MTHRUSPECIES
}

ACTOR RLCyberneticMancubusReborn : RLCyberneticMancubus
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    TNT1 A 0 A_PlaySound("volacubus/jeton", 7)
    CMAN RQPONMLK 5
    TNT1 A 0 A_ChangeFlag(FLOATBOB, 1)
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    TNT1 A 0 A_PlaySound("volacubus/jetloop", 7, 1)
    CMAN ABABCDCD 4 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedCyberneticMancubus : RLCyberneticMancubus
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCCyberneticMancubus : RLCyberneticMancubus
{
  +TOUCHY
  DeathSound "fatso/death"
  States
  {
  See:
    CMAN ABABCDCD 4
    Loop
  Missile:
    CMAN F 20
    CMAN H 5 Bright
    CMAN I 5
    CMAN FG 5
    CMAN H 5 Bright
    CMAN I 5
    CMAN FG 5
    CMAN H 5 Bright
    CMAN I 5
    CMAN FG 5
    Goto See
  Death:
    CMAN K 6
    CMAN L 6 A_Scream
    CMAN M 6 A_NoBlocking
    CMAN N 6
    CMAN OPQRS 6
    CMAN T -1 A_BossDeath
    Stop
  }
}
