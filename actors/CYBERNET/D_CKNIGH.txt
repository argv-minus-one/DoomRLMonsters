
// CYBRUISER
//
ACTOR RLCyberneticHellKnight : HellKnight 16543
{
  Health 1000 
  PainChance 50
  +MISSILEMORE
  +DONTHARMSPECIES
  SeeSound "cybruiser/sight" 
  PainSound "technolord/pain" 
  ActiveSound "technolord/active" 
  DeathSound "cybruiser/death"
  CrushPainSound "rlmonsters/crush"
  GibHealth -150
  Bloodcolor "Red"
  DamageFactor "Crush", 10
  Species "RLHellNoble"
  Obituary "%o was blown away by a Cybruiser."
  HitObituary "%o was smashed by a Cybruiser."
  Tag "Cybruiser"
  States
  {
  Spawn: 
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLHellNobleToken", 1)
    CKNI BD 10 A_Look 
    Goto Spawn+3
  See:
    TNT1 A 0 A_Playsound("cybruiser/walk", 7)
    CKNI AA 3 A_Chase
    CKNI BB 3 A_Chase
    TNT1 A 0 A_Playsound("cybruiser/walk", 7)
    CKNI CC 3 A_Chase 
    CKNI DD 3 A_Chase
    Loop 
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "MissileContinue")
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCacodemonToken", 1, "HatredOn")
	Goto HatredOff
  MissileContinue:
    CKNI EE 8 A_FaceTarget 
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    CKNI F 12 Bright A_CustomMissile("RLBruiserMissile",38,15,0,0)
    CKNI E 12 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    CKNI F 12 Bright A_CustomMissile("RLBruiserMissile",38,15,0,0)
    CKNI E 12 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    CKNI F 12 Bright A_CustomMissile("RLBruiserMissile",38,15,0,0)
    Goto See
  SummonMissile:
    CKNI F 12 Bright A_CustomMissile("RLSummonBruiserMissile",38,15,0,0)
    CKNI E 12 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    CKNI F 12 Bright A_CustomMissile("RLSummonBruiserMissile",38,15,0,0)
    CKNI E 12 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    CKNI F 12 Bright A_CustomMissile("RLSummonBruiserMissile",38,15,0,0)
    Goto See
  HatredOn:
    TNT1 A 0 A_JumpIfInventory ("PowerRLMonsterInfightCooldownToken", 1, 3)
    TNT1 A 0 A_PlaySound ("cybruiser/sight", CHAN_VOICE, 1, 0, 0.7)
    TNT1 A 0 A_GiveInventory ("PowerRLMonsterInfightCooldownToken", 1)
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 1)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 1)
    Goto MissileContinue
  HatredOff:
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 0)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 0)
    Goto MissileContinue
  Melee:
    CKNI GH 8 A_FaceTarget
    CKNI I 8 A_CustomMeleeAttack (8 * random(1, 8), "baron/melee", "rlmonsters/meleeswing", "Melee")
    Goto See
  Pain: 
    CKNI J 2 
    CKNI J 2 A_Pain 
    Goto See
  Pain.CyberDisruptor:
    CKNI J 2
    CKNI J 6 A_Pain
    CKNI J 6
    CKNI J 6 A_Pain
    CKNI J 6
    CKNI J 6 A_Pain
    CKNI J 6
    CKNI J 6 A_Pain
    Goto See
  Death: 
    CKNI K 6 Bright A_Scream
    CKNI LMN 6 Bright
    CKNI O 6 Bright A_NoBlocking
    CKNI QR 6 Bright
    CKNI S 6
    CKNI T -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    CKNI T 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    CKNI SRQONML 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    CKNI T -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLBruiserMissile
{
  Radius 8
  Height 12
  Speed 14
  Damage 10
  DamageType "Fire"
  PainType "Explosion"
  DeathType "Explosion"
  Scale 1.0
  Projectile
  RenderStyle Normal
  +THRUGHOST
  SeeSound "cybruiser/fire"
  DeathSound "rlmonsters/smallexp"
  Decal "Scorch"
  States
  {
  Spawn: 
    FBRS A 1 Bright
    FBRS A 1 Bright A_SpawnItemEx("RLBruiserTrail",0,0,0,0,0,0,0,128)
    Loop
  Death:
    BAL3 C 0 Bright A_SetTranslucent(0.67,1)
    BAL3 C 6 Bright
    BAL3 D 6 Bright A_Explode(32,64,0)
    BAL3 E 6 Bright
    Stop
  }
}

Actor RLSummonBruiserMissile : RLBruiserMissile
{
  +MTHRUSPECIES
}

Actor RLBruiserTrail
{   
  Radius 3
  Height 3
  RenderStyle Translucent
  Alpha 0.67
  Projectile
  States
  {
  Spawn:
    TNT1 A 3 Bright
    PUFF ABCD 4 BRIGHT
    Stop 
  }
}

ACTOR RLCyberneticHellKnightReborn : RLCyberneticHellKnight
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    CKNI SQRONML 8
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    CKNI BD 10 A_Look 
    Loop
  }
}

ACTOR RLDRPGSummonedCyberneticHellKnight : RLCyberneticHellKnight
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCCyberneticHellKnight : RLCyberneticHellKnight
{
  +TOUCHY
  States
  {
  Spawn: 
    CKNI BD 10 A_Look 
    Loop 
  See:
    CKNI AABBCCDD 3
    Loop
  Missile:
    CKNI E 8
    CKNI F 12 Bright
    CKNI E 12
    CKNI F 12 Bright
    CKNI E 12
    CKNI F 12 Bright
    Goto See
  Melee:
    CKNI GH 8 A_FaceTarget
    CKNI I 8 A_MeleeAttack
    Goto See
  Death: 
    CKNI K 6 Bright A_Scream
    CKNI LMN 6 Bright
    CKNI O 6 Bright A_NoBlocking
    CKNI QR 6 Bright
    CKNI S 6
    CKNI T -1
    Stop
  }
}
