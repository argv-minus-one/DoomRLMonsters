
// ARACHNOSENTINEL
//
ACTOR RLCyberneticArachnotron : Arachnotron 16545
{
  Health 1100
  GibHealth -60
  DamageFactor "Melee", 1.25
  DamageFactor "Crush", 10
  MinMissileChance 1
  PainThreshold 22
  PainChance 70
  Speed 6
  +DONTHARMSPECIES
  +QUICKTORETALIATE
  +MISSILEMORE
  +MISSILEEVENMORE
  +LOOKALLAROUND
  SeeSound "arachnosentinel/sight"
  PainSound "arachnotron/pain"
  DeathSound "arachnosentinel/death"
  CrushPainSound "rlmonsters/crush"
  Species "RLSpidermind"
  Obituary "%o strolled into the Arachnosentinel's line of fire."
  Tag "Arachnosentinel"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLSpiderMindToken", 1)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_StopSound(7)
    CARA A 1 A_Look
    Goto Spawn+5
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto See
  ClearTargetWindDown:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto MissileEnd
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_PlaySound("baby/walk", CHAN_BODY)
    CARA A 3 A_Chase
    TNT1 A 0 A_Jump (24, "Wary")
    CARA A 3 A_Chase
    TNT1 A 0 A_Jump (24, "Wary")
    CARA BBCC 3 A_Chase
    TNT1 A 0 A_PlaySound("baby/walk", CHAN_BODY)
    CARA D 3 A_Chase
    TNT1 A 0 A_Jump (24, "Wary")
    CARA D 3 A_Chase
    TNT1 A 0 A_Jump (24, "Wary")
    CARA EEFF 3 A_Chase
    Goto See
  Wary:
    TNT1 A 0 A_Jump (256, "WaryFirst", "Wary2", "WaryNoTurn")
	Goto See
  WaryNoTurn:
    CARA A 1 A_JumpIfTargetInLOS ("MissileFast", 0, JLOSF_DEADNOJUMP)
    TNT1 A 0 A_Jump (254, "WaryNoTurn")
    Goto See
  WaryFirst:
    CARA A 1 A_SetAngle(angle + 1)
    TNT1 A 0 A_JumpIfTargetInLOS ("MissileFast", 0, JLOSF_DEADNOJUMP)
    TNT1 A 0 A_Jump (254, "WaryFirst")
    Goto WarySecond
  Wary2:
    CARA A 1 A_SetAngle(angle - 1)
    TNT1 A 0 A_JumpIfTargetInLOS ("MissileFast", 0, JLOSF_DEADNOJUMP)
    TNT1 A 0 A_Jump (254, "Wary2")
    Goto WarySecond
  WarySecond:
    TNT1 A 0 A_Jump (150, "See")
  Wary3:
    TNT1 A 0 A_Jump (128, "Wary4")
    CARA A 1 A_SetAngle(angle - 1)
    TNT1 A 0 A_JumpIfTargetInLOS ("MissileFast", 0, JLOSF_DEADNOJUMP)
    TNT1 A 0 A_Jump (254, "Wary3")
    Goto See
  Wary4:
    CARA A 1 A_SetAngle(angle + 1)
    TNT1 A 0 A_JumpIfTargetInLOS ("MissileFast", 0, JLOSF_DEADNOJUMP)
    TNT1 A 0 A_Jump (254, "Wary2")
    Goto See
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelspin", 7, 1, 1, 1.2)
    TNT1 A 0 A_Chase ("", "MissileRestartFire", CHF_DONTMOVE)
    CARA Q 1 A_FaceTarget
    TNT1 A 0 A_Jump (1, "MissileEnd")
    Loop
  MissileRestartFire:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTargetWindDown")
    CARA QQQ 1 A_FaceTarget
    TNT1 A 0 A_Jump (256, 4,8,12)
    CARA QQQQQQQQQQQQQQQQ 1 A_FaceTarget
    Goto MissileFire
  MissileFast:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelwindup", 7)
    CARA Q 8 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLArachnosentinelOverwatchToken", 1)
    TNT1 A 0 A_MonsterRefire(0, "Overwatch")
	Goto MissileFire
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelwindup", 7)
    CARA QQ 16 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLArachnosentinelOverwatchToken", 1)
    TNT1 A 0 A_MonsterRefire(0, "Overwatch")
  MissileFire:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTargetWindDown")
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelspin", 7, 1, 1, 1.2)
    TNT1 A 0 A_JumpIfInventory ("RLArachnosentinelStateCounter", 1, "MissileAlt")
    TNT1 A 0 A_GiveInventory ("RLArachnosentinelStateCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/bulletstormchaingun", CHAN_WEAPON, 1, 0, 0.8)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    CARA G 3 Bright A_CustomBulletAttack (30, 5, 1, random(1,5) * 3, "RLEnemyBulletPuffNoInfight", 0, CBAF_NORANDOM)
    CARA Q 1
    TNT1 A 0 A_MonsterRefire(164, "Overwatch")
    Goto MissileFire
  SummonMissile:
    CARA G 3 Bright A_CustomBulletAttack (6, 2, 1, random(1,3) * 7, "RLEnemyBulletPuffNoInfight", 0, CBAF_NORANDOM)
    CARA Q 1
    TNT1 A 0 A_MonsterRefire(164, "Overwatch")
    Goto MissileFire
  MissileAlt:
    TNT1 A 0 A_TakeInventory ("RLArachnosentinelStateCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/bulletstormchaingun", CHAN_WEAPON, 1, 0, 0.8)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissileAlt")
    CARA H 3 Bright A_CustomBulletAttack (30, 5, 1, random(1,5) * 3, "RLEnemyBulletPuffNoInfight", 0, CBAF_NORANDOM)
    CARA Q 1
    TNT1 A 0 A_MonsterRefire(164, "Overwatch")
    Goto MissileFire
  SummonMissileAlt:
    CARA H 3 Bright A_CustomBulletAttack (6, 2, 1, random(1,3) * 7, "RLEnemyBulletPuffNoInfight", 0, CBAF_NORANDOM)
    CARA Q 1
    TNT1 A 0 A_MonsterRefire(164, "Overwatch")
    Goto MissileFire
  MissileEnd:
    TNT1 A 0 A_TakeInventory ("RLArachnosentinelOverwatchToken", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelwinddown", 7)
    CARA Q 18
    Goto See
  Pain:
    CARA I 3
    CARA I 3 A_Pain
    TNT1 A 0 A_JumpIfInventory ("RLArachnosentinelOverwatchToken", 1, "MissileRestartFire")
    TNT1 A 0 A_StopSound(7)
    Goto See
  Pain.CyberDisruptor:
    CARA I 2
    CARA I 6 A_Pain
    CARA I 6
    CARA I 6 A_Pain
    CARA I 6
    CARA I 6 A_Pain
    CARA I 6
    CARA I 6 A_Pain
    TNT1 A 0 A_TakeInventory ("RLArachnosentinelOverwatchToken", 1)
    TNT1 A 0 A_StopSound(7)
    Goto See
  Death:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_StopSound(7)
    CARA J 20 A_Scream
    CARA K 7 A_NoBlocking
    CARA LMNO 7
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    CARA P -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    CARA P 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    CARA PONMLKJ 5
    Goto See+1
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    CARA P -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLArachnosentinelOverwatchToken : Inventory
{
inventory.maxamount 1
}

Actor RLArachnosentinelStateCounter : Inventory
{
inventory.maxamount 1
}

ACTOR RLCyberneticArachnotronGuard : RLCyberneticArachnotron
{
  States
  {
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_StopSound(7)
    Goto Missile
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelspin", 7, 1, 1, 1.2)
    TNT1 A 0 A_Chase ("", "MissileRestartFire", CHF_DONTMOVE)
    CARA Q 1 A_FaceTarget
    Loop
  MissileRestartFire:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTargetWindDown")
    CARA QQQ 1 A_FaceTarget
    TNT1 A 0 A_Jump (256, 4,8,12)
    CARA QQQQQQQQQQQQQQQQ 1 A_FaceTarget
    Goto MissileFire
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("rlmonsters/sentinelwindup", 7)
    CARA QQ 16 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLArachnosentinelOverwatchToken", 1)
    TNT1 A 0 A_MonsterRefire(0, "Overwatch")
	Goto MissileFire
  }
}

ACTOR RLCyberneticArachnotronReborn : RLCyberneticArachnotron
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    CARA PONMLKJ 5
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_TakeInventory ("RLNightmareArachnotronChargeToken", 1)
    CARA ACDF 10 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedCyberneticArachnotron : RLCyberneticArachnotron
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCCyberneticArachnotron : RLCyberneticArachnotron
{
  +TOUCHY
  States
  {
  See:
    CARA AABBCC 3
    CARA DDEEFF 3
    Goto See
  Missile:
    CARA Q 4
    CARA G 3 Bright
    CARA Q 1
    CARA H 3 Bright
    CARA Q 1
    Goto Missile+1
  Death:
    CARA J 20 A_Scream
    CARA K 7 A_NoBlocking
    CARA LMNO 7
    CARA P -1 A_BossDeath
    Stop
  }
}
