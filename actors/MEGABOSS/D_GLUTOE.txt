
// GLUTTONY ELEMENTAL
//
ACTOR RLGluttonyElemental : PainElemental
{
  Health 25000
  GibHealth -10000
  MeleeThreshold 148
  MeleeRange 96
  MinMissileChance 4
  Speed 6
  Scale 1.5
  Radius 40
  Height 100
  Mass 21000
  DamageFactor "Melee", 0.4
  DamageFactor "Crush", 10
  Species "RLTheHungry"
  +BOSS
  +DONTHARMSPECIES
  +QUICKTORETALIATE
  SeeSound "agonyelemental/sight"
  ActiveSound "agonyelemental/sight"
  PainSound "agonyelemental/pain"
  DeathSound "agonyelemental/death"
  CrushPainSound "rlmonsters/crush"
  Obituary "%o was couldn't take down a Gluttony Elemental."
  HitObituary "%o was eaten by a Gluttony Elemental."
  Tag "Gluttony Elemental"
  States
  {
  Spawn:
    AGON AAA 2 A_Look
    Loop
  See:
    AGON AAABBB 2 A_Chase
    Loop
  MeleeCharge:
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 2, 0, 0)
    AGON A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    AGON A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    AGON A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    AGON A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    AGON A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    AGON A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_Stop
    Goto See
  Melee:
    TNT1 A 0 ThrustThing(angle*256/360, 2, 0, 0)
    AGON C 3 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 2, 0, 0)
    AGON D 3 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 2, 0, 0)
    AGON E 3 A_CustomMeleeAttack (10 * random(4, 10), "rlmonsters/bite", "", "Melee")
    TNT1 A 0 A_Stop
    AGON BB 2 A_Chase ("", "")
    Goto See
  Missile:
    TNT1 A 0 A_Jump (40, "TorrentSpawn")
    AGON CD 5 A_FaceTarget
    TNT1 A 0 A_FaceTarget
    AGON E 1 Bright A_SpawnItemEx ("RLTheHungryGluttonySpawner", 80, -30, 32, 0, 0, 0, 0, SXF_SETMASTER | SXF_TRANSFERPOINTERS)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_SpawnItemEx ("RLTheHungryGluttonySpawner", 80, 30, 32, 0, 0, 0, 0, SXF_SETMASTER | SXF_TRANSFERPOINTERS)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_SpawnItemEx ("RLTheHungryGluttonySpawner", 80, 0, 32, 0, 0, 0, 0, SXF_SETMASTER | SXF_TRANSFERPOINTERS)
    AGON BB 2 A_Chase ("", "")
    Goto See
  TorrentSpawn:
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", -30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 0, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", -30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 0, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", -30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 0, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", -30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 0, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", -30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 30, PAF_AIMFACING)
    AGON E 1 Bright A_FaceTarget
    AGON E 1 Bright A_PainAttack ("RLTheHungryGluttony", 0, PAF_AIMFACING)
    AGON BB 2 A_Chase ("", "")
    Goto See
  Pain:
    TNT1 A 0 A_Stop
    AGON F 6
    AGON F 6 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    AGON F 8
    AGON G 8 A_Scream
    AGON HI 8 Bright
    TNT1 A 0 A_Playsound("rlmonsters/largeexp", 7)
    TNT1 A 0 A_CustomMissile("RLAgonyElementalExplosion",32,0)
    AGON J 8 Bright
    TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx ("RLTheHungryGluttony", random(-40,40), random(-40,40), random(16,72), 0, 0, 0, 0, SXF_SETMASTER | SXF_TRANSFERPOINTERS | SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Playsound("rlmonsters/largeexp", 5)
    AGON K 8 Bright
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryGluttony", 90, 0, 24, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION)
    AGON L 8 Bright
    TNT1 A 0 A_SpawnItemEx ("RLTheHungryGluttony", -90, 0, 24, 0, 0, 0, 0, SXF_SETMASTER | SXF_NOCHECKPOSITION)
    AGON M 8 Bright A_BossDeath
    Stop
  Raise:
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedLarge",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_BossDeath
    Stop
  }
}

ACTOR RLTheHungryGluttonySpawner : RLLostSoul
{
  -COUNTKILL
  +LOOKALLAROUND
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_Look
    TNT1 A 0 A_PainAttack ("RLTheHungryGluttony", 0, PAF_AIMFACING)
    TNT1 A 0 A_Die
	Stop
  Death:
    TNT1 A 0
    Stop
  }
}

ACTOR RLTheHungryGluttony : RLArmageddonLostSoul
{
  States
  {
  Spawn:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    TNT1 A 0 A_JumpIfInventory("RLTheHungryHungers", 1, "SpawnHunt")
    TNT1 A 0 A_SetAngle(random(0,359))
    TNT1 A 0 A_Jump(200, 2)
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    TNT1 A 0 A_JumpIfInventory("RLTheHungryHungers", 1, "SpawnHunt")
    ASOL C 6 A_LookEx (LOF_NOSIGHTCHECK, 0, 0, 0, 0, "Wakeup")
    Goto Spawn+5
  }
}

ACTOR RLCCGluttonyElemental : RLGluttonyElemental
{
  +TOUCHY
  States
  {
  See:
    AGON AABB 3
    Loop
  Melee:
    Goto See
  Missile:
    AGON CD 5
    AGON E 4 Bright
    AGON E 1 Bright
    Goto See
  Death:
    AGON F 8
    AGON G 8
    AGON HI 8 Bright
    AGON J 8 Bright
    AGON K 8 Bright
    AGON L 8 Bright
    AGON M 8 Bright
    Stop
  }
}
