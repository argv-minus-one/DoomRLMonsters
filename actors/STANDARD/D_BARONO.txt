
// BARONOFHELL
//
ACTOR RLBaronOfHell : BaronOfHell
{
  GibHealth -75
  Bloodcolor "DarkGreen"
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  Species "RLHellNoble"
  +DONTHARMSPECIES
  Obituary "%o was put down by a Baron of Hell."
  HitObituary "%o was overpowered by a Baron of Hell."
  Tag "Baron of Hell"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLHellNobleToken", 1)
    BARO BD 10 A_Look
    Goto Spawn+3
  See:
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO A 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO A 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO B 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO B 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO C 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO C 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO D 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    BARO D 3 A_Chase
    Loop
  Melee:
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "MissileContinue")
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCacodemonToken", 1, "HatredOn")
	Goto HatredOff
  MissileContinue:
    TNT1 A 0 A_JumpIfInventory ("RLBaronOfHellArmToken", 1, "Missile2")
    BARO EF 8 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLBaronOfHellArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    BARO G 8 A_CustomComboAttack("RLBaronOfHellPlasmaBall", 32, 10 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire(64, "See")
    TNT1 A 0 A_JumpIfCloser (256, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  SummonMissile:
    BARO G 8 A_CustomComboAttack("RLSummonBaronOfHellPlasmaBall", 32, 10 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire(64, "See")
    TNT1 A 0 A_JumpIfCloser (256, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  Missile2:
    BARO PQ 8 A_FaceTarget
    TNT1 A 0 A_TakeInventory ("RLBaronOfHellArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile2")
    BARO R 8 A_CustomComboAttack("RLBaronOfHellPlasmaBall", 32, 10 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire(64, "See")
    TNT1 A 0 A_JumpIfCloser (256, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  SummonMissile2:
    BARO R 8 A_CustomComboAttack("RLSummonBaronOfHellPlasmaBall", 32, 10 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire(64, "See")
    TNT1 A 0 A_JumpIfCloser (256, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  HatredOn:
    TNT1 A 0 A_JumpIfInventory ("PowerRLMonsterInfightCooldownToken", 1, 3)
    TNT1 A 0 A_PlaySound ("baron/sight", CHAN_VOICE, 1, 0, 0.7)
    TNT1 A 0 A_GiveInventory ("PowerRLMonsterInfightCooldownToken", 1)
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 1)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 1)
    Goto MissileContinue
  HatredOff:
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 0)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 0)
    Goto MissileContinue
  CommandMissile:
    TNT1 A 0 A_JumpIfInventory ("RLBaronOfHellArmToken", 1, 3)
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, 1)
    Goto CommandMissile1
    BARO E 1 A_FaceTarget
    Goto CommandMissile
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, 1)
    Goto CommandMissile2
    BARO P 1 A_FaceTarget
    Goto CommandMissile
  CommandMissile1:
    TNT1 A 0 A_JumpIfInventory ("RLBaronOfHellArmToken", 1, "CommandMissile2")
    BARO EF 4 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLBaronOfHellArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonCommandMissile1")
    BARO G 4 A_CustomMissile("RLBaronOfHellPlasmaBall",32,4,frandom(0, 4))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  SummonCommandMissile1:
    BARO G 4 A_CustomMissile("RLSummonBaronOfHellPlasmaBall",32,4,frandom(0, 4))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  CommandMissile2:
    BARO PQ 4 A_FaceTarget
    TNT1 A 0 A_TakeInventory ("RLBaronOfHellArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonCommandMissile2")
    BARO R 4 A_CustomMissile("RLBaronOfHellPlasmaBall",32,-4,frandom(-4, 0))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  SummonCommandMissile2:
    BARO R 4 A_CustomMissile("RLSummonBaronOfHellPlasmaBall",32,-4,frandom(-4, 0))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  Pain:
    TNT1 A 0 A_TakeInventory ("PowerRLNightmareBaronCommand", 1)
    BARO H 2
    BARO H 2 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_Jump (128, "Death2", "Death3")
    BARO I 8
    BARO J 8 A_Scream
    BARO K 8
    BARO L 8 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    BARO MN 8
    BARO O -1
    Stop
  Death2:
    BARO S 8
    BARO T 8 A_Scream
    BARO U 8
    BARO V 8 A_NoBlocking
    BARO W 8
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    BARO X 8
    BARO Y -1
    Stop
  Death3:
    BAR2 A 8
    BAR2 B 8 A_Scream
    BAR2 C 8
    BAR2 D 8 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    BAR2 E 8
    BAR2 F 8
    BAR2 G -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    BAR2 H 1
    TNT1 A 0 A_PlaySound ("misc/gibbed", 5)
    BAR2 H 3 A_Scream
    BAR2 IJ 4
    BAR2 K 4 A_NoBlocking
    BAR2 L 4
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    BAR2 MNO 4
    BAR2 P -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    BARO O 2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    BARO O 8
    BARO NMLKJI 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_Die ("NightmareRemoval")
  Death.NightmareRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareBaronofHellReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Die ("CyberneticRemoval")
  Death.CyberneticRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticBaronofHellReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMediumGreen",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    BARO O -1 A_BossDeath
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLBaronOfHellPlasmaBall : BaronBall
{
   +THRUGHOST
   Decal "BaronScorch"
   DamageType "Plasma"
   PainType "HellNobleFire"
   DeathType "HellNobleFire"
}

ACTOR RLSummonBaronOfHellPlasmaBall : RLBaronOfHellPlasmaBall
{
   +MTHRUSPECIES
}

Actor RLBaronOfHellArmToken : Inventory
{
inventory.maxamount 1
}

ACTOR RLDRPGSummonedBaronOfHell : RLBaronOfHell
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCBaronOfHell : RLBaronOfHell
{
  +TOUCHY
  States
  {
  Spawn:
    BARO BD 10 A_Look
    Loop
  See:
    BARO AABBCCDD 3
    Loop
  Melee:
  Missile:
    BARO EF 8
    BARO G 8
    BARO PQ 8
    BARO R 8
    Goto See
  Death:
    BARO I 8
    BARO J 8 A_Scream
    BARO K 8
    BARO L 8 A_NoBlocking
    BARO MN 8
    BARO O -1 A_BossDeath
    Stop
  }
}
