
// FORMER HUMAN COMMANDO
//
ACTOR RLFormerCommandoBattleRifle : Chaingunguy
{
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was gunned down by a Former Human Commando."
  DropItem "RLBattleRiflePickup"
  +AVOIDMELEE
  Tag "Former Human Commando"
  States
  {
  Spawn:
    ZCRF AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoBattleRifleShotCounter", 20, "RetreatReload")
    ZCRF AABBCCDD 3 A_Chase
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    ZCRF AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZCRF BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZCRF CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZCRF DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    ZCRF E 10 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoBattleRifleShotCounter", 20, "RetreatReload")
    TNT1 A 0 A_GiveInventory ("RLFormerCommandoBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    ZCRF F 2 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZCRF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCommandoBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    ZCRF F 2 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZCRF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCommandoBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    ZCRF F 2 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZCRF E 2 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoBattleRifleShotCounter", 20, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCommandoBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    ZCRF F 2 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZCRF E 2 A_FaceTarget
    Goto See
  MissileEnd:
    ZCRF E 8 A_FaceTarget
    Goto RetreatReload
  Reload:
    ZCRF EE 4 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/pistolreloadout", 7)
    ZCRF EEEEEEEE 4 A_FaceTarget
    ZCRF E 2 A_FaceTarget
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("rlmonsters/pistolreloadin", 7)
    TNT1 A 0 A_TakeInventory ("RLFormerCommandoBattleRifleShotCounter", 20)
    ZCRF EE 5 A_FaceTarget
    ZCRF E 6 A_FaceTarget
    Goto See
  Pain:
    ZCRF G 3
    ZCRF G 3 A_Pain
    Goto See
  Death:
    ZCRF H 5
    ZCRF I 5 A_Scream
    ZCRF J 5 A_NoBlocking
    ZCRF K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZCRF L 5
    ZCRF M -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    ZCCH O 5
    ZCCH P 5 A_XScream
    ZCCH Q 5 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ZCCH R 5
    ZCCH S 5
    ZCCH T -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ZCRF M 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerCommandoBattleRifleShotCounter", 20)
    ZCRF L 5
    ZCRF K 5
    ZCRF J 5
    ZCRF I 5
    ZCRF H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ZCRF M -1
    Stop
  }
}

Actor RLFormerCommandoBattleRifleShotCounter : Inventory
{
inventory.maxamount 20
}

ACTOR RLCCFormerCommandoBattleRifle : RLFormerCommandoBattleRifle
{
  +TOUCHY
  States
  {
  See:
    ZCRF AABBCCDD 3
    Loop
  Missile:
    ZCRF E 10
    ZCRF F 2 Bright
    ZCRF E 2
    ZCRF F 2 Bright
    ZCRF E 2
    ZCRF F 2 Bright
    ZCRF E 10
    Goto See
  Death:
    ZCRF H 5
    ZCRF I 5 A_Scream
    ZCRF J 5 A_NoBlocking
    ZCRF K 5
    ZCRF L 5
    ZCRF M -1
    Stop
  }
}
