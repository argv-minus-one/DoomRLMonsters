
// ARACHNOTRON
//
ACTOR RLArachnotron : Arachnotron
{
  GibHealth -60
  DamageFactor "Melee", 2
  DamageFactor "Crush", 10
  PainSound "arachnotron/pain"
  CrushPainSound "rlmonsters/crush"
  +DONTHARMSPECIES
  +AVOIDMELEE
  Species "RLSpidermind"
  Obituary "%o was blasted to ashes by an Arachnotron."
  Tag "Arachnotron"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLSpiderMindToken", 1)
    ARAC ACDF 10 A_Look
    Goto Spawn+3
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto See+1
  See:
    ARAC A 20
    ARAC A 3 A_BabyMetal
    ARAC ABBCC 3 A_Chase
    ARAC D 3 A_BabyMetal
    ARAC DEEFF 3 A_Chase
    Goto See+1
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    ARAC AAAA 5 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    ARAC G 4 Bright A_CustomMissile("RLArachnotronPlasmaBallMkII", 24, 0, 0)
    ARAC H 4 Bright
    ARAC H 1 Bright A_SpidRefire
    Goto Missile+6
  SummonMissile:
    ARAC G 4 Bright A_CustomMissile("RLSummonArachnotronPlasmaBallMkII", 24, 0, 0)
    ARAC H 4 Bright
    ARAC H 1 Bright A_SpidRefire
    Goto Missile+6
  Pain:
    ARAC I 3
    ARAC I 3 A_Pain
    Goto See+1
  Pain.CyberDisruptor:
    ARAC I 2
    ARAC I 6 A_Pain
    ARAC I 6
    ARAC I 6 A_Pain
    ARAC I 6
    ARAC I 6 A_Pain
    ARAC I 6
    ARAC I 6 A_Pain
    Goto See+1
  Death:
    TNT1 A 0 A_BossDeath
    ARAC J 20 A_Scream
    ARAC K 7 A_NoBlocking
    ARAC LMNO 7
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ARAC P -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ARAC P 2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    ARAC P 5
    ARAC ONMLKJ 5
    Goto See+1
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_Die ("NightmareRemoval")
  Death.NightmareRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareArachnotronReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Die ("CyberneticRemoval")
  Death.CyberneticRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticArachnotronReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ARAC P -1 A_BossDeath
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLArachnotronPlasmaBallMkII : ArachnotronPlasma
{
   +THRUGHOST
   Alpha 0.85
   Scale 0.4
   Decal "GreenPlasmaScorch"
   DamageType "Plasma"
   SeeSound "rlmonsters/plasmamkii"
   States
   {
   Spawn:
      PL2B ACBC 4 Bright
      Loop
   Death:
	  TNT1 A 1 A_SetScale(1)
      APBX ABCDE 5 Bright
      Stop
   }
}

ACTOR RLSummonArachnotronPlasmaBallMkII : RLArachnotronPlasmaBallMkII
{
   +MTHRUSPECIES
}

ACTOR RLDRPGSummonedArachnotron : RLArachnotron
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCArachnotron : RLArachnotron
{
  +TOUCHY
  States
  {
  See:
    ARAC A 20
    ARAC AABBCC 3
    ARAC DDEEFF 3
    Goto See+1
  Missile:
    ARAC A 20
    ARAC G 4 Bright
    ARAC H 5 Bright
    ARAC G 4 Bright
    ARAC H 5 Bright
    ARAC G 4 Bright
    ARAC H 5 Bright
    Goto See
  Death:
    ARAC J 20 A_Scream
    ARAC K 7 A_NoBlocking
    ARAC LMNO 7
    ARAC P -1 A_BossDeath
    Stop
  }
}
