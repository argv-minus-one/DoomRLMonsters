
// SPECTRE
//
ACTOR RLSpectre : RLDemon
{
  +SHADOW
  RenderStyle OptFuzzy
  Alpha 0.5
  BloodType "RLSpectreBlood"
  BloodColor "Black"
  SeeSound "spectre/sight"
  AttackSound ""
  PainSound "spectre/pain"
  DeathSound "spectre/death"
  ActiveSound "spectre/active"
  CrushPainSound "rlmonsters/crush"
  DamageFactor "Crush", 10
  Species "RLDemon"
  Obituary "%o was savaged by a Spectre."
  Tag "Spectre"
  States
  {
  Spawn:
    SPEC AB 10 A_Look
    Loop
  See:
    SPEC AABBCCDD 2 Fast A_Chase
    Loop
  Melee:
    TNT1 A 0 A_PlaySound("demon/melee", CHAN_VOICE)
    SPEC EF 8 Fast A_FaceTarget
    SPEC G 8 Fast A_CustomMeleeAttack (4 * random(1, 10), "", "", "Melee")
    Goto See
  Pain:
    SPEC H 2 Fast
    SPEC H 2 Fast A_Pain
    Goto See
  Death:
    SPEC I 8
    SPEC J 8 A_Scream
    SPEC K 4
    SPEC L 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    SPEC M 4
    SPEC N -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    SPEC O 8
    SPEC P 8 A_XScream
    SPEC QR 4
    SPEC S 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    SPEC TUV 4
    SPEC W -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    SPEC N 2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    SPEC N 5
    SPEC MLKJI 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_Die ("NightmareRemoval")
  Death.NightmareRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareSpectreReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Die ("CyberneticRemoval")
  Death.CyberneticRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticSpectreReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedInvisible",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    SPEC N -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLSpectreBlood : Blood
{
  RenderStyle OptFuzzy
  Alpha 0.5
}

ACTOR RLDRPGSummonedSpectre : RLSpectre
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCSpectre : RLSpectre
{
  +TOUCHY
  States
  {
  See:
    SPEC AABBCCDD 2
    Loop
  Melee:
    SPEC EF 8
    SPEC G 8
    Goto See
  Death:
    SPEC I 8
    SPEC J 8 A_Scream
    SPEC K 4
    SPEC L 4 A_NoBlocking
    SPEC M 4
    SPEC N -1
    Stop
  }
}
