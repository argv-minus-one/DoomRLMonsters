
// LOST SOUL
//
ACTOR RLLostSoul : LostSoul
{
  Health 20
  MinMissileChance 300
  DamageFactor "Bullet", 0.5
  DamageFactor "Fire", 0.25
  DamageFactor "Crush", 10
  DamageType "Melee"
  PainType "BluntMelee"
  DeathType "BluntMelee"
  PainSound "lostsoul/pain"
  +QUICKTORETALIATE
  +NOBLOOD
  +NOINFIGHTING
  Obituary "%o was struck by a Lost Soul."
  Tag "Lost Soul"
  States
  {
  Spawn:
    LSOL AB 6 Bright A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareSoulCommand", 1, "CommandMissile")
    LSOL A 3 Bright A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareSoulCommand", 1, "CommandMissile")
    LSOL B 3 Bright A_Chase
    Loop
  Missile:
    LSOL C 10 Bright A_FaceTarget
    LSOL D 4 Bright A_SkullAttack
    LSOL CD 4 Bright
    Goto Missile+2
  CommandMissile:
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareSoulCommand", 1, 1)
    Goto CommandMissile1
    LSOL C 1 Bright A_FaceTarget
    Goto CommandMissile
  CommandMissile1:
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_Jump(128, 2)
    TNT1 A 0 A_PlaySound("skull/melee", CHAN_VOICE)
	TNT1 A 0 A_SkullAttack
    LSOL DDDD 1 Bright A_FaceTarget
    Goto Missile+2
  Pain:
    LSOL E 3 Bright
    LSOL E 3 Bright A_Pain
    Goto See
  Death:
    LSOL F 6 Bright
    LSOL G 6 Bright A_Scream
    LSOL H 6 Bright
    LSOL I 6 Bright A_NoBlocking
    LSOL J 6
    LSOL K 6
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 1)
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLLostSoulPE : RLLostSoul
{
  States
  {
  Spawn:
    TNT1 A 0 A_JumpIfInventory ("RLLostSoulPEToken", 1, "Spawn2")
    TNT1 A 0 A_GiveInventory ("RLLostSoulPEToken", 1)
    LSOL CD 4 Bright
    Goto Spawn+2
  Spawn2:
    LSOL AB 6 Bright A_Look
    Loop
  }
}

ACTOR RLLostSoulAgony : RLLostSoulPE
{
  +NOINFIGHTING
  +THRUSPECIES
  Damage (random(1,3) * 3)
}

Actor RLLostSoulPEToken : Inventory
{
inventory.maxamount 1
}

ACTOR RLDRPGSummonedLostSoul : RLLostSoul
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  }
}

ACTOR RLCCLostSoul : RLLostSoul
{
  +TOUCHY
  States
  {
  See:
    LSOL AB 6 Bright A_Chase
    Loop
  Missile:
    LSOL C 10 Bright A_FaceTarget
    LSOL D 4 Bright A_SkullAttack
    LSOL CD 4 Bright
    Goto Missile+2
  }
}
