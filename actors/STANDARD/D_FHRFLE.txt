
// FORMER HUMAN
//
ACTOR RLFormerHumanBattleRifle : ZombieMan
{
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was killed by a Former Human."
  DropItem "RLBattleRiflePickup"
  +AVOIDMELEE
  Tag "Former Human"
  States
  {
  Spawn:
    ZHRF AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerHumanBattleRifleShotCounter", 20, "RetreatReload")
    ZHRF AABBCCDD 4 A_Chase
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    ZHRF AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    ZHRF BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    ZHRF CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    ZHRF DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    ZHRF E 10 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerHumanBattleRifleShotCounter", 20, "RetreatReload")
    TNT1 A 0 A_GiveInventory ("RLFormerHumanBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifle", CHAN_WEAPON)
    ZHRF F 4 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZHRF E 8
    Goto See
  Reload:
    ZHRF EE 4 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifleunload", 7)
    ZHRF EEEEEEEE 4 A_FaceTarget
    ZHRF E 2 A_FaceTarget
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifleload", 7)
    TNT1 A 0 A_TakeInventory ("RLFormerHumanBattleRifleShotCounter", 20)
    ZHRF EE 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/battleriflecock", 5)
    ZHRF E 6 A_FaceTarget
    Goto See
  Pain:
    ZHRF G 3
    ZHRF G 3 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_Jump (128, "Death2")
    ZHRF H 5
    ZHRF I 5 A_Scream
    ZHRF J 5 A_NoBlocking
    ZHRF K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZHRF L -1
    Stop
  Death2:
    ZHRF V 8
    ZHRF W 8 A_Scream
    ZHRF X 8 A_NoBlocking
    ZHRF Y 8
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZHRF Z -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    ZHRF M 5
    ZHRF N 5 A_XScream
    ZHRF O 5 A_NoBlocking
    ZHRF PQ 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ZHRF RST 5
    ZHRF U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ZHRF L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerHumanBattleRifleShotCounter", 20)
    ZHRF K 5
    ZHRF J 5
    ZHRF I 5
    ZHRF H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ZHRF L -1
    Stop
  }
}

Actor RLFormerHumanBattleRifleShotCounter : Inventory
{
inventory.maxamount 20
}

ACTOR RLCCFormerHumanBattleRifle : RLFormerHumanBattleRifle
{
  +TOUCHY
  States
  {
  See:
    ZHRF AABBCCDD 4
    Loop
  Missile:
    ZHRF E 10
    ZHRF F 4 Bright
    ZHRF E 8
    Goto See
  Death:
    ZHRF H 5
    ZHRF I 5 A_Scream
    ZHRF J 5 A_NoBlocking
    ZHRF K 5
    ZHRF L -1
    Stop
  }
}
