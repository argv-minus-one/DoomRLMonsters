
// ARCH-VILE
//
ACTOR RLArchvile : Archvile
{
  +SEEINVISIBLE
  +DONTHARMSPECIES
  GibHealth -80
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  Obituary "%o couldn't hide from the Arch-Vile."
  Tag "Arch-Vile"
  States
  {
  Spawn:
    AVIL AB 10 A_Look
    Loop
  See:
    AVIL AABBCCDDEEFF 2 A_Chase ("", "Missile", CHF_RESURRECT)
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_VileStart
    AVIL G 10 Bright A_FaceTarget
    AVIL H 8 Bright A_VileTarget
    AVIL IJKLMN 8 Bright A_FaceTarget
    AVIL O 8 Bright A_VileAttack ("vile/stop", 20, 70, 70, 1, "Fire", VAF_DMGTYPEAPPLYTODIRECT)
    AVIL P 20 Bright
    Goto See
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    Goto See
  Heal:
    AVIH CABACABACC 3 Bright
    Goto See
  Pain:
    AVIL Q 5   
    AVIL Q 5 A_Pain
    Goto See
  Death:
    AVIL Q 7
    AVIL R 7 A_Scream
    AVIL S 7 A_NoBlocking
    AVIL TU 7
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    AVIL VWXY 7
    AVIL Z -1
    Stop
  XDeath:
    AVI2 A 5
    TNT1 A 0 A_PlaySound ("misc/gibbed", 5)
    AVI2 B 5 A_Scream
    AVI2 C 5 A_NoBlocking
    AVI2 D 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    AVI2 EFG 5
    AVI2 H -1
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    AVIL Z -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLResurrectionFire
{
  +NOBLOCKMAP
  +NOGRAVITY
  +DONTBLAST
  RenderStyle Add
  Alpha 1
  States
  {
  Spawn:
    RVFX A 2 Bright
    RVFX BAB 2 Bright
    RVFX C 2 Bright
    RVFX BCBCDCDCDEDED 2 Bright
    RVFX E 2 Bright
    RVFX FEFEFGHGHGH 2 Bright
    Stop
  }
}

ACTOR RLDRPGSummonedArchvile : RLArchvile
{
  -COUNTKILL
  -NOTARGET
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCArchvile : RLArchvile
{
  +TOUCHY
  States
  {
  Missile:
    AVIL G 0 Bright
    AVIL G 10 Bright
    AVIL H 8 Bright
    AVIL IJKLMN 8 Bright
    AVIL O 8 Bright
    AVIL P 20 Bright
    Goto See
  Melee:
    AVIH CABACABACC 3 Bright
    Goto See
  Death:
    AVIL Q 7
    AVIL R 7 A_Scream
    AVIL S 7 A_NoBlocking
    AVIL TU 7
    AVIL VWXY 7
    AVIL Z -1
  }
}
