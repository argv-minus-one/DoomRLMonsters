
// IMP
//
ACTOR RLImp : DoomImp
{
  GibHealth -40
  DamageFactor "Fire", 0.75
  DamageFactor "Crush", 10
  +DONTHARMSPECIES
  Species "RLImp"
  PainSound "imp/pain"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was burnt by an Imp."
  HitObituary "%o was slashed by an Imp."
  Tag "Imp"
  States
  {
  Spawn:
    TNT1 A 0 A_JumpIfInventory ("RLImpActiveToken", 1, "Wander")
    IMP0 AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_Jump (128, 2)
    TNT1 A 0 A_GiveInventory ("RLImpActiveToken", 1)
    IMP0 AABBCCDD 3 A_Chase
    Goto See+2
  Wander:
    TNT1 A 0 A_Look
    IMP0 A 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 A 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 B 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 B 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 C 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 C 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 D 4 A_Wander
    TNT1 A 0 A_Look
    IMP0 D 4 A_Wander
    Loop
  Melee:
  Missile:
    IMP0 EF 8 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    IMP0 G 6 A_CustomComboAttack("RLImpFireball", 32, 3 * random(1, 8), "imp/melee")
    Goto See+2
  SummonMissile:
    IMP0 G 6 A_CustomComboAttack("RLSummonImpFireball", 32, 3 * random(1, 8), "imp/melee")
    Goto See+2
  Pain:
    IMP0 H 2
    IMP0 H 2 A_Pain
    Goto See+2
  Death:
    IMP0 I 8
    IMP0 J 8 A_Scream
    IMP0 K 6
    IMP0 L 6 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    IMP0 M -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    IMP0 N 5
    IMP0 O 5 A_XScream
    IMP0 P 5
    IMP0 Q 5 A_NoBlocking
    IMP0 R 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    IMP0 ST 5
    IMP0 U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    IMP0 M 2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_TakeInventory ("RLImpActiveToken", 1)
    IMP0 MLKJI 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_Die ("NightmareRemoval")
  Death.NightmareRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareImpReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Die ("CyberneticRemoval")
  Death.CyberneticRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticImpReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    IMP0 M -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLImpActiveToken : Inventory
{
inventory.maxamount 1
}

ACTOR RLImpFireball : DoomImpBall
{
   +THRUGHOST
   Decal "DoomImpScorch"
   DamageType "Fire"
   PainType "SmallFireball"
   DeathType "SmallFireball"
}

ACTOR RLSummonImpFireball : RLImpFireball
{
   +MTHRUSPECIES
}

ACTOR RLDRPGSummonedImp : RLImp
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCImp : RLImp
{
  +TOUCHY
  States
  {
  Spawn:
    IMP0 AB 10
    Loop
  See:
    IMP0 AABBCCDD 3
    Loop
  Melee:
  Missile:
    IMP0 EF 8
    IMP0 G 6
    Goto See
  Death:
    IMP0 I 8
    IMP0 J 8 A_Scream
    IMP0 K 6
    IMP0 L 6 A_NoBlocking
    IMP0 M -1
    Stop
  }
}
