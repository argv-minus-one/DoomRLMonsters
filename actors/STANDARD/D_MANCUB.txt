
// MANCUBUS
//
ACTOR RLMancubus : Fatso
{
  GibHealth -100
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  +DONTHARMSPECIES
  Species "RLMancubus"
  Obituary "%o was incinerated by a Mancubus."
  Tag "Mancubus"
  States
  {
  Spawn:
    MANC CF 15 A_Look
    Loop
  See:
    MANC AABBCCDDEEFF 4 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfHealthLower(150, "Missile2")
    TNT1 A 0 A_PlaySound("fatso/raiseguns", CHAN_VOICE)
    MANC G 20 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusFireball", 20, -29, 20)
    MANC H 10 Bright A_CustomMissile("RLMancubusFireball", 20, 26, 0)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusFireball", 20, -29, 0)
    MANC H 10 Bright A_CustomMissile("RLMancubusFireball", 20, 26, -20)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusFireball", 20, -29, 10)
    MANC H 10 Bright A_CustomMissile("RLMancubusFireball", 20, 26, -10)
    MANC IG 5
    Goto See
  SummonMissile:
    TNT1 A 0 A_Jump(100, "SummonMissile2")
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 20)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, 0)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 0)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, -20)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 10)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, -10)
    MANC IG 5
    Goto See
  Missile2:
    TNT1 A 0 A_PlaySound("fatso/raiseguns", CHAN_VOICE)
    MANC G 20 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile2")
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 5)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, -5)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusFireball", 20, -29, 10)
    MANC H 10 Bright A_CustomMissile("RLMancubusFireball", 20, 26, -10)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusFireball", 20, -29, -20)
    MANC H 10 Bright A_CustomMissile("RLMancubusFireball", 20, 26, -20)
    MANC IG 5 A_FaceTarget
    goto See
  SummonMissile2:
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 0)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, 0)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 0)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, 0)
    MANC IG 5 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, -29, 0)
    TNT1 A 0 A_CustomMissile("RLMancubusSmoke", 20, 26, 0)
    TNT1 A 0 A_CustomMissile("RLSummonMancubusFireball", 20, -29, 0)
    MANC H 10 Bright A_CustomMissile("RLSummonMancubusFireball", 20, 26, 0)
    MANC IG 5 A_FaceTarget
    goto See
  Pain:
    MANC J 3
    MANC J 3 A_Pain
    Goto See
  Pain.CyberDisruptor:
    MANC J 2
    MANC J 6 A_Pain
    MANC J 6
    MANC J 6 A_Pain
    MANC J 6
    MANC J 6 A_Pain
    MANC J 6
    MANC J 6 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_Jump(96, "Death2")
    TNT1 A 0 A_BossDeath
    MANC K 6
    MANC L 6 A_Scream
    MANC M 6 A_NoBlocking
    MANC N 6
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    MANC OPQRS 6
    MANC T -1
    Stop
  Death2:
    TNT1 A 0 A_BossDeath
    MNC2 A 7
    MNC2 B 7 A_Scream
    MNC2 C 7 A_NoBlocking
    MNC2 DEF 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    MNC2 GHI 5
    MNC2 J -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    TNT1 A 0 A_BossDeath
    MNC2 K 4 A_XScream
    MNC2 L 4 A_NoBlocking
    MNC2 MN 4
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    MNC2 O 4
    MNC2 P -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    MANC T 2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    MANC R 5
    MANC QPONMLK 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_Die ("NightmareRemoval")
  Death.NightmareRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareMancubusReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Die ("CyberneticRemoval")
  Death.CyberneticRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticMancubusReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    MANC T -1 A_BossDeath
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLMancubusFireBall : FatShot
{
   +THRUGHOST
   Decal "Scorch"
   DamageType "Fire"
  States
  {
  Death:
    MEXP A 4 Bright
    MEXP B 4 Bright
    MEXP C 3 Bright
    MEXP D 3 Bright
    MEXP E 2 Bright
    MEXP F 2 Bright
    Stop
  }
}

ACTOR RLSummonMancubusFireBall : RLMancubusFireBall
{
   +MTHRUSPECIES
}

ACTOR RLMancubusSmoke
{
   Projectile 
   Radius 8
   Height 8
   Speed 2
   VSpeed 5
   Scale 0.4
   Damage 0
   RENDERSTYLE ADD
   Alpha 0.95
   +NOCLIP
   +NOINTERACTION
   +NOTONAUTOMAP
   States
   {
   Spawn:
   SMK2 ABCDEFGHIJKLMNOPQ 1 
   Stop
   }
}

ACTOR RLDRPGSummonedMancubus : RLMancubus
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCMancubus : RLMancubus
{
  +TOUCHY
  States
  {
  Missile:
    MANC G 20
    MANC H 10 Bright
    MANC IG 5
    MANC H 10 Bright
    MANC IG 5
    MANC H 10 Bright
    MANC IG 5
    Goto See
  Death:
    MANC K 6
    MANC L 6 A_Scream
    MANC M 6 A_NoBlocking
    MANC N 6
    MANC OPQRS 6
    MANC T -1 A_BossDeath
    Stop
  }
}
