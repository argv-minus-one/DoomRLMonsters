
// FORMER HUMAN COMMANDO
//
ACTOR RLFormerCommandoChaingun : Chaingunguy
{
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "A Former Human Commando made %o do the chaingun cha-cha."
  DropItem "RLChaingunPickup"
  +AVOIDMELEE
  Tag "Former Human Commando"
  States
  {
  Spawn:
    ZCCH AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoChaingunShotCounter", 40, "RetreatReload")
    ZCCH AABBCCDD 3 A_Chase
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    ZCCH AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZCCH BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZCCH CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZCCH DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    ZCCH U 10 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoChaingunShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCommandoChaingunShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/chaingun", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("RLFormerCommandoChaingunStateCounter", 1, "MissileAlt")
    TNT1 A 0 A_GiveInventory ("RLFormerCommandoChaingunStateCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    ZCCH F 3 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZCCH U 1 A_MonsterRefire(30, "MissileEnd")
    Goto Missile+1
  SummonMissile:
    ZCCH F 3 Bright A_CustomBulletAttack (6, 2, 1, random(1,3) * 5, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ZCCH U 1 A_MonsterRefire(30, "MissileEnd")
    Goto Missile+1
  MissileAlt:
    TNT1 A 0 A_TakeInventory ("RLFormerCommandoChaingunStateCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissileAlt")
    ZCCH E 3 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZCCH U 1 A_MonsterRefire(30, "MissileEnd")
    Goto Missile+1
  SummonMissileAlt:
    ZCCH E 3 Bright A_CustomBulletAttack (6, 2, 1, random(1,3) * 5, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ZCCH U 1 A_MonsterRefire(30, "MissileEnd")
    Goto Missile+1
  MissileEnd:
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunwinddown", 7)
    ZCCH U 10
    Goto See
  Reload:
    ZCCH UUU 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/pistolreloadout", 7)
    ZCCH UU 5 A_FaceTarget
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunload", 7)
    TNT1 A 0 A_TakeInventory ("RLFormerCommandoChaingunShotCounter", 40)
    ZCCH UUUUU 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunwinddown", 7)
    ZCCH UU 5 A_FaceTarget
    Goto See
  Pain:
    ZCCH G 3
    ZCCH G 3 A_Pain
    Goto See
  Death:
    ZCCH H 5
    ZCCH I 5 A_Scream
    ZCCH J 5 A_NoBlocking
    ZCCH K 5
    ZCCH L 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZCCH M 5
    ZCCH N -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    ZCCH O 5
    ZCCH P 5 A_XScream
    ZCCH Q 5 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ZCCH R 5
    ZCCH S 5
    ZCCH T -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ZCCH N 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerCommandoChaingunShotCounter", 40)
    ZCCH N 5
    ZCCH M 5
    ZCCH L 5
    ZCCH K 5
    ZCCH J 5
    ZCCH I 5
    ZCCH H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ZCCH N -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLFormerCommandoChaingunShotCounter : Inventory
{
inventory.maxamount 40
}

Actor RLFormerCommandoChaingunStateCounter : Inventory
{
inventory.maxamount 1
}

ACTOR RLDRPGSummonedFormerCommando : RLFormerCommandoChaingun
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCFormerCommandoChaingun : RLFormerCommandoChaingun
{
  +TOUCHY
  States
  {
  See:
    ZCCH AABBCCDD 3
    Loop
  Missile:
    ZCCH U 10
    ZCCH E 3 Bright
    ZCCH U 1
    ZCCH F 3 Bright
    ZCCH U 1
    ZCCH E 3 Bright
    ZCCH U 1
    ZCCH F 3 Bright
    ZCCH U 1
    ZCCH E 3 Bright
    ZCCH U 6
    Goto See
  Death:
    ZCCH H 5
    ZCCH I 5 A_Scream
    ZCCH J 5 A_NoBlocking
    ZCCH K 5
    ZCCH L 5
    ZCCH M 5
    ZCCH N -1
    Stop
  }
}
