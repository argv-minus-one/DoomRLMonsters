
// HELL KNIGHT
//
ACTOR RLHellKnight : HellKnight
{
  GibHealth -75
  Bloodcolor "DarkGreen"
  DamageFactor "Crush", 10
  PainSound "hellknight/pain"
  CrushPainSound "rlmonsters/crush"
  Species "RLHellNoble"
  Obituary "%o was finished off by a Hell Knight."
  HitObituary "%o was opened up like a tin can by a Hell Knight."
  Tag "Hell Knight"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLHellNobleToken", 1)
    HKNI BD 10 A_Look
    Goto Spawn+3
  See:
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI A 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI A 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI B 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI B 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI C 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI C 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI D 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    HKNI D 3 A_Chase
    Loop
  Melee:
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "MissileContinue")
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCacodemonToken", 1, "HatredOn")
	Goto HatredOff
  MissileContinue:
    TNT1 A 0 A_JumpIfInventory ("RLHellKnightArmToken", 1, "Missile2")
    HKNI EF 8 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    HKNI G 8 A_CustomComboAttack("RLHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_Jump (64, "Missile")
    Goto See
  SummonMissile:
    HKNI G 8 A_CustomComboAttack("RLSummonHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_Jump (64, "Missile")
    Goto See
  Missile2:
    HKNI PQ 8 A_FaceTarget
    TNT1 A 0 A_TakeInventory ("RLHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile2")
    HKNI R 8 A_CustomComboAttack("RLHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_Jump (64, "Missile")
    Goto See
  SummonMissile2:
    HKNI R 8 A_CustomComboAttack("RLSummonHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_Jump (64, "Missile")
    Goto See
  HatredOn:
    TNT1 A 0 A_JumpIfInventory ("PowerRLMonsterInfightCooldownToken", 1, 3)
    TNT1 A 0 A_PlaySound ("knight/sight", CHAN_VOICE, 1, 0, 0.7)
    TNT1 A 0 A_GiveInventory ("PowerRLMonsterInfightCooldownToken", 1)
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 1)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 1)
    Goto MissileContinue
  HatredOff:
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 0)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 0)
    Goto MissileContinue
  CommandMissile:
    TNT1 A 0 A_JumpIfInventory ("RLHellKnightArmToken", 1, 3)
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, 1)
    Goto CommandMissile1
    HKNI E 1 A_FaceTarget
    Goto CommandMissile
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, 1)
    Goto CommandMissile1
    HKNI P 1 A_FaceTarget
    Goto CommandMissile
  CommandMissile1:
    TNT1 A 0 A_JumpIfInventory ("RLHellKnightArmToken", 1, "CommandMissile2")
    HKNI EF 4 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonCommandMissile1")
    HKNI G 4 A_CustomMissile("RLHellKnightPlasmaBall",32,4,frandom(0, 4))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  SummonCommandMissile1:
    HKNI G 4 A_CustomMissile("RLSummonHellKnightPlasmaBall",32,4,frandom(0, 4))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  CommandMissile2:
    HKNI PQ 4 A_FaceTarget
    TNT1 A 0 A_TakeInventory ("RLHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonCommandMissile2")
    HKNI R 4 A_CustomMissile("RLHellKnightPlasmaBall",32,-4,frandom(-4, 0))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  SummonCommandMissile2:
    HKNI R 4 A_CustomMissile("RLSummonHellKnightPlasmaBall",32,-4,frandom(-4, 0))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  Pain:
    TNT1 A 0 A_TakeInventory ("PowerRLNightmareBaronCommand", 1)
    HKNI H 2
    HKNI H 2 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_Jump (96, "Death2")
    HKNI I 8
    HKNI J 8 A_Scream
    HKNI K 8
    HKNI L 8 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    HKNI MN 8
    HKNI O -1
    Stop
  Death2:
    HKNI S 8
    HKNI T 8 A_Scream
    HKNI UV 8
    HKNI W 8 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    HKNI X 8
    HKNI Y -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    HKN2 A 1
    TNT1 A 0 A_PlaySound ("misc/gibbed", 5)
    HKN2 A 3 A_Scream
    HKN2 BC 4
    HKN2 D 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    HKN2 E 4
    HKN2 FGH 4
    HKN2 I -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    HKNI O 2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    HKNI O 8
    HKNI NMLKJI 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_Die ("NightmareRemoval")
  Death.NightmareRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareHellKnightReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Die ("CyberneticRemoval")
  Death.CyberneticRemoval:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticHellKnightReborn", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMediumGreen",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    HKNI O -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLHellKnightPlasmaBall : BaronBall
{
   +THRUGHOST
   Decal "BaronScorch"
   DamageType "Plasma"
   PainType "HellNobleFire"
   DeathType "HellNobleFire"
   Damage 6
}

ACTOR RLSummonHellKnightPlasmaBall : RLHellKnightPlasmaBall
{
   +MTHRUSPECIES
}

Actor RLHellKnightArmToken : Inventory
{
inventory.maxamount 1
}

ACTOR RLDRPGSummonedHellKnight : RLHellKnight
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCHellKnight : RLHellKnight
{
  +TOUCHY
  States
  {
  Spawn:
    HKNI BD 10 A_Look
    Loop
  See:
    HKNI AABBCCDD 3
    Loop
  Melee:
    HKNI PQ 8
    HKNI R 8
    Goto See
  Missile:
    HKNI EF 8
    HKNI G 8
    Goto See
  Death:
    HKNI I 8
    HKNI J 8 A_Scream
    HKNI K 8
    HKNI L 8 A_NoBlocking
    HKNI MN 8
    HKNI O -1
    Stop
  }
}
