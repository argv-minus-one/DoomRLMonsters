
// FORMER HUMAN CAPTAIN
//
ACTOR RLFormerCaptainPlasmaRifle : Zombieman 16511
{
  Health 100
  DamageFactor "Bullet", 0.85
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "A Former Human Captain burnt %o into a pile of bloody ashes."
  DropItem "RLPlasmaRiflePickup"
  +AVOIDMELEE
  Tag "Former Human Captain"
  States
  {
  Spawn:
    ZXPL AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "RetreatReload")
    ZXPL AABBCCDD 3 A_Chase
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    ZXPL AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZXPL BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZXPL CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ZXPL DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    ZXPL EE 5 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    ZXPL F 2 Bright A_CustomMissile ("RLFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    ZXPL F 2 Bright A_CustomMissile ("RLFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    ZXPL F 2 Bright A_CustomMissile ("RLFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    ZXPL F 2 Bright A_CustomMissile ("RLFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    Goto MissileEnd
  SummonMissile:
    ZXPL F 2 Bright A_CustomMissile ("RLSummonFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    ZXPL F 2 Bright A_CustomMissile ("RLSummonFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    ZXPL F 2 Bright A_CustomMissile ("RLSummonFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    TNT1 A 0 A_JumpIfInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLFormerCaptainPlasmaRifleShotCounter", 1)
    TNT1 A 0 A_FaceTarget
    ZXPL F 2 Bright A_CustomMissile ("RLSummonFormerCaptainPlasmaBall", 30, 13, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    ZXPL E 1 A_MonsterRefire(30, "MissileEnd")
    Goto MissileEnd
  MissileEnd:
    ZXPL E 9
    Goto See
  Reload:
    ZXPL EEEEEE 4 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/plasmarifleunload", 7)
    ZXPL E 4 A_FaceTarget
    ZXPL E 5 A_FaceTarget
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("rlmonsters/plasmarifleload", 7)
    TNT1 A 0 A_TakeInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40)
    ZXPL EEEEEE 4 A_FaceTarget
    Goto See
  Pain:
    ZXPL G 3
    ZXPL G 3 A_Pain
    Goto See
  Death:
    ZXPL H 5
    ZXPL I 5 A_Scream
    ZXPL J 5 A_NoBlocking
    ZXPL K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZXPL L -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    ZXPL M 5
    ZXPL N 5 A_XScream
    ZXPL O 5 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ZXPL PQRS 5
    ZXPL T -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ZXPL L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerCaptainPlasmaRifleShotCounter", 40)
    ZXPL LKJIH 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ZXPL L -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLFormerCaptainPlasmaRifleShotCounter : Inventory
{
inventory.maxamount 40
}

ACTOR RLFormerCaptainPlasmaBall : PlasmaBall
{
  +THRUGHOST
  +STRIFEDAMAGE
  Alpha 0.85
  Decal "PlasmaScorch"
  Damagetype "Plasma"
  Damage 4
}

ACTOR RLSummonFormerCaptainPlasmaBall : RLFormerCaptainPlasmaBall
{
  +MTHRUSPECIES
  Damage 5
}

ACTOR RLDRPGSummonedFormerCaptain : RLFormerCaptainPlasmaRifle
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCFormerCaptainPlasmaRifle : RLFormerCaptainPlasmaRifle
{
  +TOUCHY
  States
  {
  See:
    ZXPL AABBCCDD 3
    Loop
  Missile:
    ZXPL E 10
    ZXPL F 2 Bright
    ZXPL E 1
    ZXPL F 2 Bright
    ZXPL E 1
    ZXPL F 2 Bright
    ZXPL E 1
    ZXPL F 2 Bright
    ZXPL E 10
    Goto See
  Death:
    ZXPL H 5
    ZXPL I 5 A_Scream
    ZXPL J 5 A_NoBlocking
    ZXPL K 5
    ZXPL L -1
    Stop
  }
}
