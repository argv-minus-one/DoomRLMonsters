
// FORMER HUMAN SERGEANT
//
ACTOR RLFormerSergeantShotgun : ShotgunGuy
{
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was shot apart by a Former Human Sergeant."
  DropItem "RLShotgunPickup"
  +AVOIDMELEE
  Tag "Former Human Sergeant"
  States
  {
  Spawn:
    ZSSH AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerSergeantShotgunShotCounter", 1, "RetreatReload")
    ZSSH AABBCCDD 3 A_Chase
    Loop
  RetreatReload:
    ZSSH AABBCCDD 3 A_Chase ("", "Reload")
    Goto RetreatReload
  Missile:
    ZSSH E 10 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerSergeantShotgunShotCounter", 1, "RetreatReload")
    TNT1 A 0 A_GiveInventory ("RLFormerSergeantShotgunShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/shotgun", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    ZSSH F 10 Bright A_CustomBulletAttack (22.5, 0, 3, random(1,5)*3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ZSSH E 10
    TNT1 A 0 A_Jump (128, "Reload")
    Goto See
  SummonMissile:
    ZSSH F 10 Bright A_CustomBulletAttack (6.5, 3, 7, random(1,3)*5, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ZSSH E 10
    TNT1 A 0 A_Jump (128, "Reload")
    Goto See
  Reload:
    ZSSH VV 4 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/shotgunpump", 7)
    TNT1 A 0 A_TakeInventory ("RLFormerSergeantShotgunShotCounter", 1)
    ZSSH WW 4 A_FaceTarget
    ZSSH VV 4 A_FaceTarget
    Goto See
  Pain:
    ZSSH G 3
    ZSSH G 3 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_Jump(128, "Death2")
    ZSSH H 5
    ZSSH I 5 A_Scream
    ZSSH J 5 A_NoBlocking
    ZSSH K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZSSH L -1
    Stop
  Death2:
    ZSS2 A 8
    ZSS2 B 8 A_Scream
    ZSS2 C 8 A_NoBlocking
    ZSS2 D 8
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ZSS2 E -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    ZSSH M 5
    ZSSH N 5 A_XScream
    ZSSH O 5 A_NoBlocking
    ZSSH PQ 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ZSSH RST 5
    ZSSH U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ZSSH L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerSergeantShotgunShotCounter", 1)
    ZSSH K 5
    ZSSH J 5
    ZSSH I 5
    ZSSH H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ZSSH L -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLFormerSergeantShotgunShotCounter : Inventory
{
inventory.maxamount 1
}

ACTOR RLDRPGSummonedFormerSergeant : RLFormerSergeantShotgun
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCFormerSergeantShotgun : RLFormerSergeantShotgun
{
  +TOUCHY
  States
  {
  See:
    ZSSH AABBCCDD 3
    Loop
  Missile:
    ZSSH E 10
    ZSSH F 10 Bright
    ZSSH E 10
    ZSSH V 8
    ZSSH W 8
    ZSSH V 8
    Goto See
  Death:
    ZSSH H 5
    ZSSH I 5 A_Scream
    ZSSH J 5 A_NoBlocking
    ZSSH K 5
    ZSSH L -1
    Stop
  }
}
