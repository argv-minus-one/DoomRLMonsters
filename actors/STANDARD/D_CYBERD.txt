
// CYBERDEMON
//
ACTOR RLCyberdemon : Cyberdemon
{
  -NORADIUSDMG
  +NOFEAR
  +EXTREMEDEATH
  +DONTHARMSPECIES
  DamageType "Melee"
  PainType "BluntMelee"
  DeathType "BluntMelee"
  MinMissileChance 10
  PainThreshold 60
  PainChance 250
  MeleeRange 120
  Mass 10000
  Speed 20
  RadiusDamageFactor 0.2
  DamageFactor "Melee", 0.5
  DamageFactor "Radiation", 0
  DamageFactor "Crush", 10
  CrushPainSound "rlmonsters/crush"
  PainSound "cyberdemon/pain"
  Species "RLCyberdemon"
  Obituary "%o was pasted across the walls by a Cyberdemon."
  HitObituary "%o was crushed into paste by a Cyberdemon."
  DropItem "RLTacticalRocketLauncherPickup" 1
  Tag "Cyberdemon"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLCyberdemonToken", 1)
    VCYB C 10 A_Look
    Goto Spawn+3
  See:
    TNT1 A 0 A_JumpIfInTargetInventory("RLInquisitorsSetBonusActive", 1, "FireproofSee")
    VCYB DD 4 A_Chase
    TNT1 A 0 A_PlaySound("cyber/hoof", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(2, 6, 0, 512, "")
    VCYB AA 4 A_Chase ("Melee", "", CHF_DONTMOVE)
    TNT1 A 0 A_PlaySound("spider/walk", 5, 1, 0, 0.7)
    VCYB BB 4 A_Chase
    TNT1 A 0 A_PlaySound("rlmonsters/metalhoof", 5, 0.7, 0, 0.7)
    TNT1 A 0 A_Quake(2, 6, 0, 512, "")
    VCYB CC 4 A_Chase ("Melee", "Missile", CHF_DONTMOVE)
    Loop
  FireproofSee:
    VCYB DD 4 A_Chase ("Melee", "")
    TNT1 A 0 A_PlaySound("cyber/hoof", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(2, 6, 0, 512, "")
    VCYB AA 4 A_Chase ("Melee", "", CHF_DONTMOVE)
    TNT1 A 0 A_PlaySound("spider/walk", 5, 1, 0, 0.7)
    VCYB BB 4 A_Chase ("Melee", "")
    TNT1 A 0 A_PlaySound("cyber/hoof", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(2, 6, 0, 512, "")
    VCYB CC 4 A_Chase ("Melee", "FireproofMissile", CHF_DONTMOVE)
    Goto See
  Melee:
    TNT1 A 0 A_JumpIfInTargetInventory("RLTacticalSetBonusActive", 1, "MeleeAOECheck")
    TNT1 A 0 A_CheckFlag("BOSS", "Missile", AAPTR_TARGET)
  MeleeCheckComplete:
    VCYB Q 12 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/stomp", 5, 2, 0, 0.6)
    TNT1 A 0 A_Quake(3, 12, 0, 1024, "")
    TNT1 A 0 A_Quake(9, 12, 0, 512, "")
    TNT1 A 0 A_CustomMissile("RLStompSmoke", 10, 20, 0)
    TNT1 A 0 A_JumpIfCloser(72, 1)
    Goto MeleeContinue
    TNT1 A 0 A_CustomMeleeAttack (512 * random(1, 2), "", "", "Melee")
  MeleeContinue:
    TNT1 A 0 A_CustomMissile("RLCyberStompShockwave",5,0)
    VCYB R 12 A_RadiusThrust(512, 164, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
    VCYB C 12 A_FaceTarget
    Goto See
  MeleeAOECheck:
    TNT1 A 0 A_JumpIfCloser(72, 1)
    Goto Missile
    TNT1 A 0
    Goto MeleeCheckComplete
  Missile:
    VCYB E 6 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    Goto See
  SummonMissile:
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    Goto See
  FireproofMissile:
    VCYB E 6 A_FaceTarget
    TNT1 A 0 A_Jump(128, "FireproofMissile2")
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonFireproofMissile")
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(5,2), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    Goto See
  SummonFireproofMissile:
    VCYB F 12 Bright A_CustomMissile("RLSummonCyberdemonRocket", 50, -20, frandom(5,2), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    Goto See
  FireproofMissile2:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonFireproofMissile2")
    VCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(-5,-2), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    Goto See
  SummonFireproofMissile2:
    VCYB F 12 Bright A_CustomMissile("RLSummonCyberdemonRocket", 50, -20, frandom(-5,-2), CMF_OFFSETPITCH, frandom(1,-1))
    VCYB E 12 A_FaceTarget
    Goto See
  Pain:
    VCYB G 10 A_Pain
    Goto See
  Pain.CyberDisruptor:
    VCYB G 2
    VCYB G 6 A_Pain
    VCYB G 6
    VCYB G 6 A_Pain
    VCYB G 6
    VCYB G 6 A_Pain
    VCYB G 6
    VCYB G 6 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    VCYB H 10
    TNT1 A 0 A_Quake(7, 40, 0, 1024, "")
    VCYB I 10 Bright A_Scream
    VCYB JKL 10 Bright
    TNT1 A 0 A_Quake(4, 20, 0, 1024, "")
    VCYB M 10 Bright A_NoBlocking
    VCYB NO 10
    VCYB P 30
    VCYB P -1 A_BossDeath
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedLarge",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    VCYB P -1 A_BossDeath
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLCyberdemonRocket : Rocket
{
   +THRUGHOST
   Decal "Scorch"
   DamageType "Fire"
   PainType "Explosion"
   DeathType "Explosion"
  States
  {
  Death:
    MEXP A 4 Bright A_Explode (128,128)
    MEXP B 4 Bright
    MEXP C 3 Bright
    MEXP D 3 Bright
    MEXP E 2 Bright
    MEXP F 2 Bright
    Stop
  }
}

ACTOR RLSummonCyberdemonRocket : RLCyberdemonRocket
{
   +MTHRUSPECIES
}

ACTOR RLCyberStompShockwave : Rocket
{
   Radius 1
   Height 1
   +MTHRUSPECIES
   +THRUGHOST
   -RANDOMIZE
   -DEHEXPLOSION
   -ROCKETTRAIL
   DamageType "Melee"
   PainType "BluntMelee"
   DeathType "BluntMelee"
   Speed 0
   Damage 0
   SeeSound ""
   DeathSound ""
   Obituary "%o was crushed into paste by a Cyberdemon."
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_Explode(128, 128, "None", 0, 128)
    TNT1 A 8
    Stop
  }
}

ACTOR RLStompSmoke
{
   Radius 8
   Height 8
   VSpeed 5
   Scale 0.8
   Damage 0
   RENDERSTYLE ADD
   Alpha 0.25
   +NOINTERACTION
   +NOTONAUTOMAP
   States
   {
   Spawn:
   SMK2 ABCDEFGHIJKLMNOPQ 2 A_FadeTo (0, 0.05, 1)
   Stop
   }
}

ACTOR RLDRPGSummonedCyberdemon : RLCyberdemon
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  }
}

ACTOR RLCCCyberdemon : RLCyberdemon
{
  +TOUCHY
  States
  {
  See:
    VCYB DD 4
    VCYB AA 4
    VCYB BB 4
    VCYB CC 4
    Loop
  Melee:
    VCYB Q 12
    VCYB R 12
    VCYB C 12
    Goto See
  Missile:
    VCYB E 6
    VCYB F 12 Bright
    VCYB E 12
    VCYB F 12 Bright
    VCYB E 12
    VCYB F 12 Bright
    Goto See
  Death:
    VCYB H 10
    VCYB I 10 Bright A_Scream
    VCYB JKL 10 Bright
    VCYB M 10 Bright A_NoBlocking
    VCYB NO 10
    VCYB P 30
    VCYB P -1 A_BossDeath
    Stop
  }
}
