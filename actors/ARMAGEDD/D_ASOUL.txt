
// THE HUNGRY
//
ACTOR RLArmageddonLostSoul : LostSoul 16559
{
  Health 10
  Radius 14
  Height 30
  Mass 80
  Speed 4
  Damage 2
  Scale 0.7
  Painchance 224
  BloodColor "Blue"
  MinMissileChance 0
  RenderStyle Normal
  DamageFactor "Crush", 10
  DamageType "Melee"
  PainType "BluntMelee"
  DeathType "BluntMelee"
  SeeSound "thehungry/sight"
  ActiveSound "thehungry/active"
  AttackSound "thehungry/attack"
  PainSound "thehungry/pain"
  DeathSound "thehungry/death"
  Species "RLTheHungry"
  +QUICKTORETALIATE
  +NOBLOOD
  +THRUSPECIES
  +MISSILEMORE
  +MISSILEEVENMORE
  +THRUGHOST
  +NOFEAR
  +SEEINVISIBLE
  +SPAWNFLOAT
  -CANUSEWALLS
  -NOBLOOD
  +FIXMAPTHINGPOS
  Obituary "%o was consumed by The Hungry."
  Tag "The Hungry"
  States
  {
  Spawn:
    TNT1 A 0 A_JumpIfInventory("RLTheHungryHungers", 1, "SpawnHunt")
    TNT1 A 0 A_SetAngle(random(0,359))
    TNT1 A 0 A_Jump(200, 2)
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    TNT1 A 0 A_JumpIfInventory("RLTheHungryHungers", 1, "SpawnHunt")
    ASOL C 6 A_LookEx (LOF_NOSIGHTCHECK, 0, 0, 0, 0, "Wakeup")
    Goto Spawn+4
  Wakeup:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    ASOL D 6
    Goto See
  See:
    ASOL AAAABBBB random(1,3) A_Chase
    Loop
  SpawnHunt:
    TNT1 A 0 A_SetAngle(random(0,359))
    TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * 6.4, 0, Sin(Pitch) * -6.4, 1)
    ASOL B 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL B 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_ScaleVelocity (0.9)
    ASOL A 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    TNT1 A 0 A_Stop
    ASOL AABBAABB 2 A_LookEx ("None", 0, 0, 0, 360, "See")
    Loop
  Melee:
  Missile:
    ASOL BA 2 A_FaceTarget
    ASOL F 2 A_SkullAttack (35)
    ASOL E 2
  MissileLoop:
    ASOL FABAFE 2
    Loop
  Pain:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    ASOL L 3 
    ASOL L 3 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
    ASOL I 5
    ASOL J 5 A_Scream
    ASOL K 5
    ASOL L 5 A_NoBlocking
    ASOL MN 5 A_SetFloorClip
    ASOL O 500
  DeathLoop:
    ASOL O 1 A_CheckSight("FadeAway")
    Loop
  FadeAway:
    ASOL O 1 A_FadeTo(0, 0.1, 1)
    Loop
  XDeath:
    TNT1 A 0 A_GiveInventory("RLTheHungryHungers", 1)
    ASOL P 3 
    ASOL Q 3 A_XScream
    ASOL R 3 A_NoBlocking  
    ASOL STU 3
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    Stop
  }
}

Actor RLTheHungryHungers : Inventory
{
inventory.maxamount 1
}

actor RLTheHungrySpawner
{
    Radius 14
    Height 30
	+NOBLOCKMAP
    States
    {
    Spawn:
	    TNT1 A 0 NoDelay A_Jump (256, 1,2,3,4,5,6)
	    TNT1 AAAAAAAAAAA 0 A_SpawnItemEx ("RLTheHungrySingleSpawner", random(-64,64), random(-64,64), random(0,16))
		Stop
    }
}

actor RLTheHungrySingleSpawner
{
    Radius 14
    Height 30
    Speed 15
    Species "RLTheHungry"
	+SOLID
	+NOBLOCKMAP
	+NOTONAUTOMAP
	+THRUSPECIES
    States
    {
    Spawn:
	    TNT1 A 0
	    TNT1 AAAAAAA 0 A_Wander
	    TNT1 A 0 A_SpawnItemEx ("RLArmageddonLostSoul", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Stop
    }
}

ACTOR RLCCArmageddonLostSoul : RLArmageddonLostSoul
{
  +TOUCHY
  States
  {
  See:
    ASOL AAAABBBB random(1,3)
    Loop
  Melee:
  Missile:
    ASOL BAFEFA 2
    Loop
  Death:
    ASOL I 5
    ASOL J 5 A_Scream
    ASOL K 5
    ASOL L 5 A_NoBlocking
    ASOL MN 5 A_SetFloorClip
    ASOL O -1
	Stop
  }
}
