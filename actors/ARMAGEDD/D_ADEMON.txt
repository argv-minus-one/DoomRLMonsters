
// MECH-STROSITY
//"I swear to God If that's a hitscan weapon, I'll murder you." - insightguy, 2014
ACTOR RLArmageddonDemon : Demon 16557
{
  Health 500
  PainChance 75
  PainChance "Melee", 160
  GibHealth -90
  DamageFactor "Crush", 10
  SeeSound "mechstrosity/sight"
  PainSound "mechstrosity/pain"
  DeathSound "mechstrosity/death"
  ActiveSound "mechstrosity/active"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  +DONTHARMSPECIES
  +QUICKTORETALIATE
  Species "RLDemon"
  Obituary "%o was unable to stop a Mech-strosity in time."
  Tag "Mech-strosity"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLDemonToken", 1)
    ADEM AB 10 A_Look
    Goto Spawn+3
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto Spawn
  See:
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfHealthLower (300, "Chase")
    TNT1 A 0 A_PlaySound("mechdemon/walk", 7)
    ADEM AA 2 Fast A_Chase
    ADEM BB 2 Fast A_Chase
    TNT1 A 0 A_PlaySound("mechdemon/walk", 7)
    ADEM CC 2 Fast A_Chase
    ADEM DD 2 Fast A_Chase
    Goto See
  Chase:
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_SetSpeed(12)
    TNT1 A 0 A_PlaySound("mechdemon/walk", 7)
    ADEM AA 1 Fast A_Chase
    ADEM BB 1 Fast A_Chase
    TNT1 A 0 A_PlaySound("mechdemon/walk", 7)
    ADEM CC 1 Fast A_Chase
    ADEM DD 1 Fast A_Chase
    Goto See
  Melee:
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("mechstrosity/attack", CHAN_VOICE)
    ADEM EF 6 Fast A_FaceTarget
    ADEM G 6 Fast A_CustomMeleeAttack (5 * random(1, 10), "", "", "Melee")
    Goto See
  Pain:
    TNT1 A 0 A_JumpIfHealthLower (300, 1)
    Goto Pain+2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    TNT1 A 0 A_JumpIfInTargetInventory ("PowerRLMechstrosityOverwatch", 1, "PainOverwatch")
    ADEM H 3 Fast
    ADEM H 3 Fast A_Pain
    TNT1 A 0 A_JumpIfHealthLower (300, "Chase")
    TNT1 A 0 A_JumpIfCloser (1024, "OverwatchStart")
    Goto See
  Pain.Melee:
    TNT1 A 0 A_JumpIfHealthLower (300, 1)
    Goto Pain.Melee+2
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    TNT1 A 0 A_JumpIfInTargetInventory ("PowerRLMechstrosityOverwatch", 1, "PainOverwatch")
    ADEM H 2 Fast
    ADEM H 2 Fast A_Pain
    TNT1 A 0 A_JumpIfHealthLower (300, "Chase")
    Goto See
  PainOverwatch:
    ADEM R 4 Fast
    ADEM R 4 Fast A_Pain
    TNT1 A 0 A_JumpIfHealthLower (300, "Chase")
    TNT1 A 0 A_JumpIfCloser (1024, "Overwatch")
    Goto See
  Pain.CyberDisruptor:
    TNT1 A 0 A_JumpIfInTargetInventory ("PowerRLMechstrosityOverwatch", 1, "PainOverwatchDisrupted")
    ADEM H 2
    ADEM H 6 A_Pain
    ADEM H 6
    ADEM H 6 A_Pain
    ADEM H 6
    ADEM H 6 A_Pain
    ADEM H 6
    ADEM H 6 A_Pain
    Goto See
  PainOverwatchDisrupted:
    TNT1 A 0 A_TakeInventory ("PowerRLMechstrosityOverwatch", 1)
    ADEM R 2
    ADEM R 6 A_Pain
    ADEM R 6
    ADEM R 6 A_Pain
    ADEM R 6
    ADEM R 6 A_Pain
    ADEM R 6
    ADEM R 6 A_Pain
    Goto See
//Oh look it shoots you have fun (°∀°)
  OverwatchStart:
    TNT1 A 0 A_GiveInventory ("PowerRLMechstrosityOverwatch", 1)
  Overwatch:
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfInventory ("PowerRLMechstrosityOverwatch", 1, 1)
    Goto See
    ADEM O 0 A_Chase ("", "MissileRestartFire", CHF_DONTMOVE)
    ADEM O 1 A_FaceTarget
    ADEM O 0 A_Chase ("", "MissileRestartFire", CHF_DONTMOVE)
    ADEM O 1 A_FaceTarget
    TNT1 A 0 A_JumpIfCloser (1024, "Overwatch")
    ADEM O 0 A_MonsterRefire(0, "See")
    Loop
  MissileRestartFire:
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    ADEM O 2 A_FaceTarget
    TNT1 A 0 A_Jump (256, 1,2,3,4)
    ADEM OOOO 2 A_FaceTarget
    Goto MissileFire
  MissileFire:
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("rlmonsters/chaingun", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("RLMechstrosityStateCounter", 1, "MissileAlt")
    TNT1 A 0 A_GiveInventory ("RLMechstrosityStateCounter", 1)
    ADEM P 3 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "RLEnemyBulletPuffNoInfight", 0, CBAF_NORANDOM)
    ADEM O 1 A_MonsterRefire(100, "Overwatch")
    TNT1 A 0 A_JumpIfInventory ("PowerRLMechstrosityOverwatch", 1, "MissileFire")
    Goto See
  MissileAlt:
    TNT1 A 0 A_TakeInventory ("RLMechstrosityStateCounter", 1)
    ADEM Q 3 Bright A_CustomBulletAttack (22.5, 0, 1, random(1,5) * 3, "RLEnemyBulletPuffNoInfight", 0, CBAF_NORANDOM)
    ADEM O 1 A_MonsterRefire(100, "Overwatch")
    TNT1 A 0 A_JumpIfInventory ("PowerRLMechstrosityOverwatch", 1, "MissileFire")
    Goto See
  Death:
    ADEM I 8
    ADEM J 8 A_Scream
    ADEM K 4
    ADEM L 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ADEM M 4
    ADEM N -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ADEM N 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    ADEM NMLKJI 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ADEM N -1
    Stop
  }
}

Actor RLMechstrosityStateCounter : Inventory
{
inventory.maxamount 1
}

ACTOR PowerRLMechstrosityOverwatch : Powerup
{
	Powerup.Duration -6
   +INVENTORY.UNTOSSABLE
}

ACTOR RLCCArmageddonDemon : RLArmageddonDemon
{
  +TOUCHY
  States
  {
  Spawn:
    ADEM AB 10
    Loop
  See:
    ADEM AABBCCDD 2
    Loop
  Missile:
    ADEM EF 6
    ADEM G 6
    Goto See
  Melee:
    ADEM H 8
    ADEM Q 3
    ADEM O 1
    ADEM P 3
    ADEM O 1
    ADEM Q 3
    ADEM O 1
    ADEM P 3
    ADEM R 8
    Goto See
  Death:
    ADEM I 8
    ADEM J 8 A_Scream
    ADEM K 4
    ADEM L 4 A_NoBlocking
    ADEM M 4
    ADEM N -1
    Stop
  }
}
