
// FORMER OVERWATCH
//"Yay, Riker zombies!" - Seryder, 2014
ACTOR RLFormerOverwatch : ShotgunGuy 16553
{
  Health 90
  PainChance 190
  PainThreshold 5
  DamageFactor "Crush", 10
  DamageFactor "Bullet", 0.85
  SeeSound "eliteformer/sight"
  PainSound "eliteformer/pain"
  DeathSound "eliteformer/death"
  ActiveSound "eliteformer/active"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was blasted to shreds by a Former Overwatch's super shotgun."
  DropItem "RLShell"
  DropItem "RLShell"
  DropItem "RLSuperShotgunPickup", 30
  +AVOIDMELEE
  Tag "Former Overwatch"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_Jump (220, "NoDrone")
    TNT1 A 0 A_JumpIfInventory ("RLFormerOverwatchDroneControl", 1, "Spawn2")
    TNT1 A 0 A_SpawnItemEx ("RLUACDefenceDrone", -48, random(-32,32), random(12,32), 0, 0, 0, 0, SXF_SETMASTER)
  Spawn2:
    AZSG AB 10 A_Look
    Loop
  NoDrone:
    TNT1 A 0 A_GiveInventory ("RLFormerOverwatchDroneControl", 1)
    Goto Spawn2
  See:
    TNT1 A 0 A_JumpIfInventory ("RLFormerOverwatchSuperShotgunShotCounter", 1, "RetreatReload")
    AZSG A 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See1
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See1:
    AZSG A 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See2
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See2:
    AZSG B 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See3
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See3:
    AZSG B 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See4
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See4:
    AZSG C 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See5
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See5:
    AZSG C 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See6
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See6:
    AZSG D 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See7
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
  See7:
    AZSG D 3 A_Chase
    TNT1 A 0 A_CheckSight (1)
	Goto See
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
	Goto See
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    AZSG AA 2 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    AZSG BB 2 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    AZSG CC 2 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    AZSG DD 2 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("RLFormerOverwatchSuperShotgunShotCounter", 1, "Reload")
    TNT1 A 0 A_Chase ("", "MissileAmbush", CHF_DONTMOVE)
    AZSG E 3 A_FaceTarget
    TNT1 A 0 A_JumpIfCloser (300, "Overwatch")
    Goto See
  Missile:
    AZSG EEE 5 A_FaceTarget
  MissileAmbush:
    AZSG E random(3,10) A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLFormerOverwatchSuperShotgunShotCounter", 1, "RetreatReload")
    TNT1 A 0 A_GiveInventory ("RLFormerOverwatchSuperShotgunShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/supershotgun", CHAN_WEAPON, 1, 0, 0.7)
    AZSG F 10 Bright A_CustomBulletAttack (40, 10, 10, random(1,5)*3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    AZSG EE 5 A_FaceTarget
    Goto See
  Reload:
    AZSG EE 7 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/supershotgunopen", 7)
    AZSG E 21
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerOverwatchSuperShotgunShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/supershotgunload", 7)
    AZSG E 18
    TNT1 A 0 A_PlaySound("rlmonsters/supershotgunclose", 7)
    AZSG E 5 A_FaceTarget
    Goto See
  Pain:
    AZSG G 3
    AZSG G 3 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_KillChildren
    AZSG H 5
    AZSG I 5 A_Scream
    AZSG J 5 A_NoBlocking
    AZSG K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    AZSG L -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    TNT1 A 0 A_KillChildren
    AZSG M 5
    AZSG N 5 A_XScream
    AZSG O 5 A_NoBlocking
    AZSG P 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    AZSG QRST 5
    AZSG U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    AZSG L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLFormerOverwatchSuperShotgunShotCounter", 1)
    AZSG K 5
    AZSG J 5
    AZSG I 5
    AZSG H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    AZSG L -1
    Stop
  }
}

Actor RLFormerOverwatchSuperShotgunShotCounter : Inventory
{
inventory.maxamount 1
}

Actor RLFormerOverwatchDroneControl : Inventory
{
inventory.maxamount 1
}

ACTOR RLCCFormerOverwatch : RLFormerOverwatch
{
  +TOUCHY
  States
  {
  See:
    AZSG AABBCCDD 3
    Loop
  Missile:
    AZSG E 10
    AZSG F 10 Bright
    AZSG E 24
    AZSG E 21
    AZSG E 18
    AZSG E 5
    Goto See
  Death:
    AZSG H 5
    AZSG I 5 A_Scream
    AZSG J 5 A_NoBlocking
    AZSG K 5
    AZSG L -1
    Stop
  }
}
