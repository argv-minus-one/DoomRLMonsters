
// NIGHTMARE MANCUBUS
//
ACTOR RLNightmareMancubus : Fatso 16531
{
  Health 900
  GibHealth -100
  DamageFactor "Plasma", 0.75
  DamageFactor "Crush", 10
  BloodColor "Dark Red"
  +DONTHARMSPECIES
  Species "RLMancubus"
  SeeSound "nightmaremancubus/sight"
  PainSound "nightmaremancubus/pain"
  DeathSound "nightmaremancubus/death"
  ActiveSound "eliteformer/active"
  CrushPainSound "rlmonsters/crush"
  Obituary "%o was pulverized by a Nightmare Mancubus."
  Tag "Nightmare Mancubus"
  States
  {
  Spawn:
    NMAN CF 15 A_Look
    Loop
  See:
    NMAN AABBCCDDEEFF 4 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfHealthLower(200, "Missile2")
    TNT1 A 0 A_PlaySound("nightmaremancubus/attack", CHAN_VOICE)
    NMAN G 20 A_FaceTarget
    TNT1 A 0 A_Jump(80, "MissileBurst")
  MissileContinue:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    NMAN U 8 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, 28, frandom(4,-4))
    NMAN IG 3 A_FaceTarget
    NMAN V 8 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, -31, frandom(4,-4))
    NMAN I 3 A_FaceTarget
    NMAN G 3 A_MonsterRefire(80, "See")
    Goto MissileContinue
  SummonMissile:
    NMAN U 8 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, 28, frandom(4,-4))
    NMAN IG 3 A_FaceTarget
    NMAN V 8 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, -31, frandom(4,-4))
    NMAN I 3 A_FaceTarget
    NMAN G 3 A_MonsterRefire(80, "See")
    Goto MissileContinue
  MissileBurst:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissileBurst")
    NMAN U 4 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, 28, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    NMAN V 4 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, -31, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    NMAN U 4 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, 28, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    NMAN V 4 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, -31, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    Goto See
  SummonMissileBurst:
    NMAN U 4 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, 28, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    NMAN V 4 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, -31, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    NMAN U 4 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, 28, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    NMAN V 4 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, -31, frandom(1,-1))
    NMAN IG 2 A_FaceTarget
    Goto See
  Missile2:
    TNT1 A 0 A_PlaySound("nightmaremancubus/attack", CHAN_VOICE)
    NMAN G 20 A_FaceTarget
  Missile2Continue:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile2")
    TNT1 A 0 A_CustomMissile("RLNightmareMancubusPlasmaball", 20, -31, frandom(5,-5))
    NMAN H 10 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, 28, frandom(5,-5))
    NMAN I 5 A_FaceTarget
    NMAN G 5 A_MonsterRefire(80, "See")
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLNightmareMancubusPlasmaball", 20, -31, frandom(10,-10))
    NMAN H 10 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, 28, frandom(10,-10))
    NMAN I 5 A_FaceTarget
    NMAN G 5 A_MonsterRefire(80, "See")
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLNightmareMancubusPlasmaball", 20, -31, frandom(15,-15))
    NMAN H 10 Bright A_CustomMissile("RLNightmareMancubusPlasmaball", 20, 28, frandom(15,-15))
    NMAN I 5 A_FaceTarget
    NMAN G 5 A_MonsterRefire(80, "See")
    Goto Missile2Continue
  SummonMissile2:
    TNT1 A 0 A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, -31, frandom(5,-5))
    NMAN H 10 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, 28, frandom(5,-5))
    NMAN I 5 A_FaceTarget
    NMAN G 5 A_MonsterRefire(80, "See")
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, -31, frandom(10,-10))
    NMAN H 10 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, 28, frandom(10,-10))
    NMAN I 5 A_FaceTarget
    NMAN G 5 A_MonsterRefire(80, "See")
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, -31, frandom(15,-15))
    NMAN H 10 Bright A_CustomMissile("RLSummonNightmareMancubusPlasmaball", 20, 28, frandom(15,-15))
    NMAN I 5 A_FaceTarget
    NMAN G 5 A_MonsterRefire(80, "See")
    Goto Missile2Continue
  Pain:
    NMAN J 3
    NMAN J 3 A_Pain
    Goto See
  Pain.CyberDisruptor:
    NMAN J 2
    NMAN J 6 A_Pain
    NMAN J 6
    NMAN J 6 A_Pain
    NMAN J 6
    NMAN J 6 A_Pain
    NMAN J 6
    NMAN J 6 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_BossDeath
    NMAN K 6
    NMAN L 6 A_Scream
    NMAN M 6 A_NoBlocking
    NMAN N 6
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    NMAN OPQRS 6
    NMAN T -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    NMAN T 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    NMAN RQPONMLK 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    NMAN T -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLNightmareMancubusPlasmaball : BFGBall
{
  Radius 13
  Height 8
  Speed 28
  Damage (50)
  DamageType "Plasma"
  PainType "PlasmaExplosion"
  DeathType "PlasmaExplosion"
  Projectile
  +RANDOMIZE
  +THRUGHOST
  RenderStyle Add
  Alpha 0.85
  Scale 0.6
  Decal "EnemyRedBFGLightning"
  SeeSound "nightmaremancubus/fire"
  DeathSound "nightmaremancubus/explode"
  States
  {
  Spawn:
    NMBX ABCDEFGH 1 Bright A_SpawnItem("RLNightmareMancubusPlasmaballTrail",0,0)
    Loop
  Death:
    TNT1 A 0 A_SetScale (1)
    TNT1 A 0 A_Explode (16, 96, "None")
    NMBX IJK 4 Bright
    TNT1 A 0 A_Explode (1, 128, "None", 0, 128)
    NMBX L 2 Bright A_SetScale (1.4, 1)
    TNT1 A 0 A_Explode (1, 144, "None", 0, 144)
    NMBX L 2 Bright A_SetScale (1.8, 1)
    TNT1 A 0 A_Explode (1, 160, "None", 0, 160)
    NMBX M 2 Bright A_SetScale (2.2, 1)
    TNT1 A 0 A_Explode (1, 176, "None", 0, 176)
    NMBX M 2 Bright A_SetScale (2.6, 1)
    TNT1 A 0 A_Explode (1, 192, "None", 0, 192)
    NMBX N 2 Bright A_SetScale (3.0, 1)
    TNT1 A 0 A_Explode (1, 208, "None", 0, 208)
    NMBX N 2 Bright A_SetScale (3.4, 1)
    Stop
  }
}

ACTOR RLSummonNightmareMancubusPlasmaball : RLNightmareMancubusPlasmaball
{
  +MTHRUSPECIES
}

ACTOR RLNightmareMancubusPlasmaballTrail
{   
   Radius 2
   Height 2
   Speed 0
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.35
   +NOINTERACTION
   States
   {
   Spawn:
      TNT1 A 3
      BBB3 ABCDE 1 Bright
      Stop 
   }
}

ACTOR RLNightmareMancubusReborn : RLNightmareMancubus
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    NMAN RQPONMLK 5
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    NMAN CF 15 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedNightmareMancubus : RLNightmareMancubus
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCNightmareMancubus : RLNightmareMancubus
{
  +TOUCHY
  States
  {
  Missile:
    NMAN G 20
    NMAN U 8 Bright
    NMAN IG 3
    NMAN V 8 Bright
    NMAN IG 3
    NMAN H 10 Bright
    NMAN IG 5
    Goto See
  Death:
    NMAN K 6
    NMAN L 6 A_Scream
    NMAN M 6 A_NoBlocking
    NMAN N 6
    NMAN OPQRS 6
    NMAN T -1 A_BossDeath
    Stop
  }
}
