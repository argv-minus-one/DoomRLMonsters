
// NIGHTMARE KNIGHT
//
ACTOR RLNightmareHellKnight : HellKnight 16526
{
  Health 650
  GibHealth -75
  Mass 1200
  Bloodcolor "DarkGreen"
  DamageFactor "Crush", 10
  PainSound "hellknight/pain"
  Species "RLHellNoble"
  SeeSound "nightmareknight/sight"
  ActiveSound "nightmaredemon/active"
  PainSound "nightmaredemon/pain"
  DeathSound "nightmareknight/death"
  CrushPainSound "rlmonsters/crush"
  Obituary "%o took a face-full of burning green from a Nightmare Knight."
  HitObituary "%o got bitch-slapped by a Nightmare Knight."
  Tag "Nightmare Knight"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLHellNobleToken", 1)
    NKNI BD 10 A_Look
    Goto Spawn+3
  See:
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI A 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI A 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI B 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI B 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI C 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI C 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI D 3 A_Chase
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, "CommandMissile")
    NKNI D 3 A_Chase
    Loop
  Melee:
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "MissileContinue")
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCacodemonToken", 1, "HatredOn")
	Goto HatredOff
  MissileContinue:
    TNT1 A 0 A_JumpIfInventory ("RLNightmareHellKnightArmToken", 1, "Missile2")
    NKNI EF 6 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLNightmareHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    NKNI G 6 A_CustomComboAttack("RLNightmareHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire (0, "See")
    TNT1 A 0 A_JumpIfCloser (512, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  SummonMissile:
    NKNI G 6 A_CustomComboAttack("RLSummonNightmareHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire (0, "See")
    TNT1 A 0 A_JumpIfCloser (512, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  Missile2:
    NKNI PQ 6 A_FaceTarget
    TNT1 A 0 A_TakeInventory ("RLNightmareHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile2")
    NKNI R 6 A_CustomComboAttack("RLNightmareHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire (0, "See")
    TNT1 A 0 A_JumpIfCloser (512, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  SummonMissile2:
    NKNI R 6 A_CustomComboAttack("RLSummonNightmareHellKnightPlasmaBall", 32, 8 * random(1, 8), "baron/melee")
    TNT1 A 0 A_MonsterRefire (0, "See")
    TNT1 A 0 A_JumpIfCloser (512, 1)
    Goto See
    TNT1 A 0
    Goto Missile
  HatredOn:
    TNT1 A 0 A_JumpIfInventory ("PowerRLMonsterInfightCooldownToken", 1, 3)
    TNT1 A 0 A_PlaySound ("nightmareknight/sight", CHAN_VOICE, 1, 0, 0.7)
    TNT1 A 0 A_GiveInventory ("PowerRLMonsterInfightCooldownToken", 1)
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 1)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 1)
    Goto MissileContinue
  HatredOff:
    TNT1 A 0 A_ChangeFlag ("NOFEAR", 0)
    TNT1 A 0 A_ChangeFlag ("NOTARGETSWITCH", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 0)
    TNT1 A 0 A_ChangeFlag ("MISSILEEVENMORE", 0)
    Goto MissileContinue
  CommandMissile:
    TNT1 A 0 A_JumpIfInventory ("RLNightmareHellKnightArmToken", 1, 3)
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, 1)
    Goto CommandMissile1
    NKNI E 1 A_FaceTarget
    Goto CommandMissile
    TNT1 A 0 A_JumpIfInventory("PowerRLNightmareBaronCommand", 1, 1)
    Goto CommandMissile2
    NKNI P 1 A_FaceTarget
    Goto CommandMissile
  CommandMissile1:
    TNT1 A 0 A_JumpIfInventory ("RLNightmareHellKnightArmToken", 1, "CommandMissile2")
    NKNI EF 4 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLNightmareHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonCommandMissile1")
    NKNI G 4 A_CustomMissile("RLNightmareHellKnightPlasmaBall",32,4,frandom(0, 4))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  SummonCommandMissile1:
    NKNI G 4 A_CustomMissile("RLSummonNightmareHellKnightPlasmaBall",32,4,frandom(0, 4))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  CommandMissile2:
    NKNI PQ 4 A_FaceTarget
    TNT1 A 0 A_TakeInventory ("RLNightmareHellKnightArmToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonCommandMissile2")
    NKNI R 4 A_CustomMissile("RLNightmareHellKnightPlasmaBall",32,-4,frandom(-4, 0))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  SummonCommandMissile2:
    NKNI R 4 A_CustomMissile("RLSummonNightmareHellKnightPlasmaBall",32,-4,frandom(-4, 0))
    TNT1 A 0 A_MonsterRefire (0, "See")
    Goto CommandMissile1
  Pain:
    TNT1 A 0 A_TakeInventory ("PowerRLNightmareBaronCommand", 1)
    NKNI H 2
    NKNI H 2 A_Pain
    Goto See
  Death:
    NKNI I 8
    NKNI J 8 A_Scream
    NKNI K 8
    NKNI L 8 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    NKNI MN 8
    NKNI O -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    NKNI O 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    NKNI O 8
    NKNI NMLKJI 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMediumGreen",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    NKNI O -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLNightmareHellKnightPlasmaBall : BaronBall
{
   +THRUGHOST
   Decal "BaronScorch"
   DamageType "Plasma"
   PainType "HellNobleFire"
   DeathType "HellNobleFire"
   Speed 18
   FastSpeed 25
   Damage 8
}

ACTOR RLSummonNightmareHellKnightPlasmaBall : RLNightmareHellKnightPlasmaBall
{
   +MTHRUSPECIES
}

Actor RLNightmareHellKnightArmToken : Inventory
{
inventory.maxamount 1
}

ACTOR RLNightmareHellKnightReborn : RLNightmareHellKnight
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    TNT1 A 0 A_GiveInventory ("RLHellNobleToken", 1)
    NKNI ONMLKJI 5
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    NKNI AB 10 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedNightmareHellKnight : RLNightmareHellKnight
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCNightmareHellKnight : RLNightmareHellKnight
{
  +TOUCHY
  States
  {
  Spawn:
    NKNI BD 10 A_Look
    Loop
  See:
    NKNI AABBCCDD 3
    Loop
  Melee:
    NKNI PQ 6
    NKNI R 6
    Goto See
  Missile:
    NKNI EF 6
    NKNI G 6
    Goto See
  Death:
    NKNI I 8
    NKNI J 8 A_Scream
    NKNI K 8
    NKNI L 8 A_NoBlocking
    NKNI MN 8
    NKNI O -1
    Stop
  }
}
