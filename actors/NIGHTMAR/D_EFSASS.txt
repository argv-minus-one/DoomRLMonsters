
// ELITE FORMER HUMAN SERGEANT
//
ACTOR RLEliteSergeantAssaultShotgun : ShotgunGuy 16517
{
  Health 60
  DamageFactor "Crush", 10
  DamageFactor "Bullet", 0.85
  SeeSound "eliteformer/sight"
  PainSound "eliteformer/pain"
  DeathSound "eliteformer/death"
  ActiveSound "eliteformer/active"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was assaulted by an Elite Human Sergeant."
  DropItem "RLShellBox"
  DropItem "RLAssaultShotgunPickup" 30
  +AVOIDMELEE
  Tag "Elite Human Sergeant"
  States
  {
  Spawn:
    EASS AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLEliteSergeantAssaultShotgunShotCounter", 6, "RetreatReload")
    EASS AABBCCDD 3 A_Chase
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    EASS AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    EASS BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    EASS CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    EASS DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    EASS EE 5 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLEliteSergeantAssaultShotgunShotCounter", 6, "RetreatReload")
    TNT1 A 0 A_GiveInventory ("RLEliteSergeantAssaultShotgunShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/assaultshotgun", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    EASS F 10 Bright A_CustomBulletAttack (22.5, 0, 3, random(1,5)*3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    EASS E 5 A_FaceTarget
    EASS E 5 A_MonsterRefire(10, "See")
    Goto Missile+2
  SummonMissile:
    EASS F 10 Bright A_CustomBulletAttack (4.5, 1, 7, random(1,2)*5, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    EASS E 5 A_FaceTarget
    EASS E 5 A_MonsterRefire(10, "See")
    Goto Missile+2
  Reload:
    EASS EEE 4 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/pistolreloadout", 7)
    EASS E 53
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLEliteSergeantAssaultShotgunShotCounter", 6)
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunload", 7)
    EASS EEEE 3 A_FaceTarget
    Goto See
  Pain:
    EASS G 3
    EASS G 3 A_Pain
    Goto See
  Death:
    EASS H 5
    EASS I 5 A_Scream
    EASS J 5 A_NoBlocking
    EASS K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    EASS L -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    EASS M 5
    ZHRF N 5 A_XScream
    ZHRF O 5 A_NoBlocking
    ZHRF PQ 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ZHRF RST 5
    ZHRF U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    EASS L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLEliteSergeantAssaultShotgunShotCounter", 6)
    EASS K 5
    EASS J 5
    EASS I 5
    EASS H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    EASS L -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLEliteSergeantAssaultShotgunShotCounter : Inventory
{
inventory.maxamount 6
}

ACTOR RLDRPGSummonedEliteSergeant : RLEliteSergeantAssaultShotgun
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCEliteSergeantAssaultShotgun : RLEliteSergeantAssaultShotgun
{
  +TOUCHY
  States
  {
  See:
    EASS AABBCCDD 3
    Loop
  Missile:
    EASS E 10
    EASS F 7 Bright
    EASS E 5
    EASS F 7 Bright
    EASS E 5
    EASS F 7 Bright
    EASS E 10
    Goto See
  Death:
    EASS H 5
    EASS I 5 A_Scream
    EASS J 5 A_NoBlocking
    EASS K 5
    EASS L -1
    Stop
  }
}
