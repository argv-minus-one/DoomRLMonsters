
// ELITE FORMER HUMAN
//
ACTOR RLEliteHumanBattleRifle : ZombieMan
{
  Health 40
  DamageFactor "Crush", 10
  DamageFactor "Bullet", 0.85
  SeeSound "eliteformer/sight"
  PainSound "eliteformer/pain"
  DeathSound "eliteformer/death"
  ActiveSound "eliteformer/active"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was eliminated by an Elite Former Human."
  DropItem "RLClip"
  DropItem "RLBattleRiflePickup"
  +AVOIDMELEE
  Tag "Elite Human"
  States
  {
  Spawn:
    ERFL AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_JumpIfInventory ("RLEliteHumanBattleRifleShotCounter", 40, "RetreatReload")
    ERFL AABBCCDD 4 A_Chase
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    ERFL AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    ERFL BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    ERFL CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    ERFL DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (32, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  Missile:
    TNT1 A 0 A_JumpIfInventory ("RLEliteHumanBattleRifleShotCounter", 40, "RetreatReload")
    ERFL E 12 A_FaceTarget
    TNT1 A 0 A_GiveInventory ("RLEliteHumanBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/burstbattlerifle", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    ERFL F 1 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 4, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ERFL E 1 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLEliteHumanBattleRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLEliteHumanBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/burstbattlerifle", CHAN_WEAPON)
    ERFL F 1 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 4, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ERFL E 1 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLEliteHumanBattleRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLEliteHumanBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/burstbattlerifle", CHAN_WEAPON)
    ERFL F 1 Bright A_CustomBulletAttack (16, 0, 1, random(1,5) * 4, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ERFL E 1 A_FaceTarget
	Goto MissileEnd
  SummonMissile:
    ERFL F 1 Bright A_CustomBulletAttack (3, 1.5, 1, random(1,3) * 10, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ERFL E 1 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLEliteHumanBattleRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLEliteHumanBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/burstbattlerifle", CHAN_WEAPON)
    ERFL F 1 Bright A_CustomBulletAttack (3, 1.5, 1, random(1,3) * 10, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ERFL E 1 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("RLEliteHumanBattleRifleShotCounter", 40, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLEliteHumanBattleRifleShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/burstbattlerifle", CHAN_WEAPON)
    ERFL F 1 Bright A_CustomBulletAttack (3, 1.5, 1, random(1,3) * 10, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ERFL E 1 A_FaceTarget
	Goto MissileEnd
  MissileEnd:
    ERFL E 8 A_FaceTarget
    Goto See
  Reload:
    ERFL EE 4 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifleunload", 7)
    ERFL EEEEEEEE 4 A_FaceTarget
    ERFL E 2 A_FaceTarget
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("rlmonsters/battlerifleload", 7)
    TNT1 A 0 A_TakeInventory ("RLEliteHumanBattleRifleShotCounter", 40)
    ERFL EE 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/battleriflecock", 5)
    ERFL E 6 A_FaceTarget
    Goto See
  Pain:
    ERFL G 3
    ERFL G 3 A_Pain
    Goto See
  Death:
    ERFL H 5
    ERFL I 5 A_Scream
    ERFL J 5 A_NoBlocking
    ERFL K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ERFL L -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    ERFL M 5
    ERFL N 5 A_XScream
    ERFL O 5 A_NoBlocking
    ERFL PQ 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ERFL RST 5
    ERFL U -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ERFL L 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLEliteHumanBattleRifleShotCounter", 40)
    ERFL K 5
    ERFL J 5
    ERFL I 5
    ERFL H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ERFL L -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLEliteHumanBattleRifleShotCounter : Inventory
{
inventory.maxamount 20
}

ACTOR RLDRPGSummonedEliteHuman : RLEliteHumanBattleRifle
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCEliteHumanBattleRifle : RLEliteHumanBattleRifle
{
  +TOUCHY
  States
  {
  See:
    ERFL AABBCCDD 4
    Loop
  Missile:
    ERFL E 5
    ERFL F 1 Bright
    ERFL E 1
    ERFL F 1 Bright
    ERFL E 1
    ERFL F 1 Bright
    ERFL E 9
    Goto See
  Death:
    ERFL H 5
    ERFL I 5 A_Scream
    ERFL J 5 A_NoBlocking
    ERFL K 5
    ERFL L -1
    Stop
  }
}
