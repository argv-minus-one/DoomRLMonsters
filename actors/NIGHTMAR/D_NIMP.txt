
// NIGHTMARE IMP
//
ACTOR RLNightmareImp : DoomImp 16521
{
  Health 90
  DamageFactor "Fire", 0.50
  DamageFactor "Crush", 10
  +DONTHARMSPECIES
  SeeSound "nightmareimp/sight"
  ActiveSound "nightmareimp/active"
  PainSound "imp/pain"
  DeathSound "nightmareimp/death"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Species "RLImp"
  Obituary "%o was taken down by a Nightmare Imp."
  HitObituary "%o was lacerated by a Nightmare Imp."
  Tag "Nightmare Imp"
  States
  {
  Spawn:
    TNT1 A 0 A_FadeTo (1, 1, 0)
    TNT1 A 0 A_Look
    NIMP A 4 A_Wander
    TNT1 A 0 A_Look
    NIMP A 4 A_Wander
    TNT1 A 0 A_Look
    NIMP B 4 A_Wander
    TNT1 A 0 A_Look
    NIMP B 4 A_Wander
    TNT1 A 0 A_Look
    NIMP C 4 A_Wander
    TNT1 A 0 A_Look
    NIMP C 4 A_Wander
    TNT1 A 0 A_Look
    NIMP D 4 A_Wander
    TNT1 A 0 A_Look
    NIMP D 4 A_Wander
    Loop
  See:
    TNT1 A 0 A_CheckSight ("InvisibleSeeA")
    TNT1 A 0 A_FadeTo (1, 0.1, 0)
    NIMP AA 3 A_Chase
    Goto SeeB
  SeeB:
    TNT1 A 0 A_CheckSight ("InvisibleSeeB")
    TNT1 A 0 A_FadeTo (1, 0.1, 0)
    NIMP BB 3 A_Chase
    Goto SeeC
  SeeC:
    TNT1 A 0 A_CheckSight ("InvisibleSeeC")
    TNT1 A 0 A_FadeTo (1, 0.1, 0)
    NIMP CC 3 A_Chase
    Goto SeeD
  SeeD:
    TNT1 A 0 A_CheckSight ("InvisibleSeeD")
    TNT1 A 0 A_FadeTo (1, 0.1, 0)
    NIMP DD 3 A_Chase
    Goto See
  InvisibleSeeA:
    NIMP AA 4 A_Chase
    TNT1 A 0 A_FadeTo (0.1, 0.1, 0)
    TNT1 A 0 A_CheckSight ("InvisibleSeeB")
    Goto SeeB
  InvisibleSeeB:
    NIMP BB 4 A_Chase
    TNT1 A 0 A_FadeTo (0.1, 0.1, 0)
    TNT1 A 0 A_CheckSight ("InvisibleSeeC")
    Goto SeeC
  InvisibleSeeC:
    NIMP CC 4 A_Chase
    TNT1 A 0 A_FadeTo (0.1, 0.1, 0)
    TNT1 A 0 A_CheckSight ("InvisibleSeeD")
    Goto SeeD
  InvisibleSeeD:
    NIMP DD 4 A_Chase
    TNT1 A 0 A_FadeTo (0.1, 0.1, 0)
    TNT1 A 0 A_CheckSight ("InvisibleSeeA")
    Goto See
  Melee:
    TNT1 A 0 A_FadeTo (1, 1, 0)
    NIMP WX 8 A_FaceTarget
    NIMP Y 6 A_CustomMeleeAttack (3 * random(1, 8), "imp/melee", "", "Melee")
    Goto See
  Missile:
    TNT1 A 0 A_FadeTo (1, 1, 0)
    NIMP EF 8 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    NIMP G 6 A_CustomMissile("RLNightmareImpFireball", 32)
    Goto See
  SummonMissile:
    NIMP G 6 A_CustomMissile("RLSummonNightmareImpFireball", 32)
    Goto See
  Pain:
    TNT1 A 0 A_FadeTo (1, 1, 0)
    NIMP H 2
    NIMP H 2 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_FadeTo (1, 1, 0)
    NIMP I 8
    NIMP J 8 A_Scream
    NIMP K 6
    NIMP L 6 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    NIMP M 6
    NIMP N -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    TNT1 A 0 A_FadeTo (1, 1, 0)
    NIMP O 5
    NIMP P 5 A_XScream
    NIMP Q 5
    NIMP R 5 A_NoBlocking
    NIMP S 5
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    NIMP TU 5
    NIMP V -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    NIMP N 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    NIMP MLKJI 8
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    NIMP N -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLNightmareImpFireball : BaronBall
{
   +THRUGHOST
   Scale 0.8
   DamageType "Fire"
   PainType "SmallFireball"
   DeathType "SmallFireball"
   Damage 5
   States
   {
   Spawn:
    NIMB AB 4 Bright
    Loop
   Death:
    NIMB CDE 6 Bright
    Stop
   }
}

ACTOR RLSummonNightmareImpFireball : RLNightmareImpFireball
{
   +MTHRUSPECIES
}

ACTOR RLNightmareImpShadowball : BaronBall
{
   +THRUGHOST
   +RIPPER
   +BLOODLESSIMPACT
   Speed 10
   FastSpeed 10
   Decal "BaronScorch"
   DamageType "Plasma"
   Damage (1)
   RenderStyle Fuzzy
   Alpha 0.5
   States
   {
   Spawn:
    SMK2 ABCDEFGHIJKLMNOPQ 1 
    Goto Death
   Death:
    TNT1 A 0
    Stop
   }
}

ACTOR RLNightmareImpReborn : RLNightmareImp
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    NIMP MLKJI 8
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    TNT1 A 0 A_FadeTo (1, 1, 0)
    TNT1 A 0 A_Look
    NIMP A 4 A_Wander
    TNT1 A 0 A_Look
    NIMP A 4 A_Wander
    TNT1 A 0 A_Look
    NIMP B 4 A_Wander
    TNT1 A 0 A_Look
    NIMP B 4 A_Wander
    TNT1 A 0 A_Look
    NIMP C 4 A_Wander
    TNT1 A 0 A_Look
    NIMP C 4 A_Wander
    TNT1 A 0 A_Look
    NIMP D 4 A_Wander
    TNT1 A 0 A_Look
    NIMP D 4 A_Wander
    Loop
  }
}

ACTOR RLDRPGSummonedNightmareImp : RLNightmareImp
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCNightmareImp : RLNightmareImp
{
  +TOUCHY
  States
  {
  Spawn:
    NIMP AB 4
    Loop
  See:
    NIMP AABBCCDD 3
    Loop
  Melee:
    NIMP WX 8
    NIMP Y 6
    Goto See
  Missile:
    NIMP EF 8
    NIMP G 6
    Goto See
  Death:
    NIMP I 8
    NIMP J 8 A_Scream
    NIMP K 6
    NIMP L 6 A_NoBlocking
    NIMP M 6
    NIMP N -1
    Stop
  }
}
