
// NIGHTMARE CYBERDEMON
//"10 tons of fuck you" - Yholl, 2014
ACTOR RLNightmareCyberdemon : Cyberdemon 16533
{
  -NORADIUSDMG
  +NOFEAR
  +EXTREMEDEATH
  +DONTHARMSPECIES
  DamageType "Melee"
  PainType "BluntMelee"
  DeathType "BluntMelee"
  Health 10000
  PainThreshold 100
  PainChance 250
  MeleeRange 120
  Mass 10000
  Speed 20
  RadiusDamageFactor 0.2
  DamageFactor "Melee", 0.5
  DamageFactor "Radiation", 0
  DamageFactor "Crush", 10
  SeeSound "nightmarecyberdemon/sight"
  ActiveSound "nightmarecyberdemon/active"
  PainSound "nightmarecyberdemon/pain"
  CrushPainSound "rlmonsters/crush"
  Species "RLCyberdemon"
  Obituary "%o screamed as the Nightmare Cyberdemon destroyed %h without mercy."
  HitObituary "%o was just another corpse beneath the Nightmare Cyberdemon's hoof."
  DropItem "RLParticleBeamCannonPickup" 1
  DropItem "RLPlasmaRedirectionCannonPickup" 1
  Tag "Nightmare Cyberdemon"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLCyberdemonToken", 1)
    NCYB C 10 A_Look
    Goto Spawn+3
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    NCYB DD 4 A_Chase
    TNT1 A 0 A_PlaySound("cyber/hoof", 5, 1, 0, 0.7)
    TNT1 A 0 A_Quake(2, 6, 0, 512, "")
    NCYB AA 4 A_Chase ("Melee", "", CHF_DONTMOVE)
    TNT1 A 0 A_PlaySound("spider/walk", 5, 1, 0, 0.7)
    NCYB BB 4 A_Chase
    TNT1 A 0 A_PlaySound("rlmonsters/metalhoof", 5, 0.7, 0, 0.7)
    TNT1 A 0 A_Quake(2, 6, 0, 512, "")
    NCYB CC 4 A_Chase ("Melee", "Missile", CHF_DONTMOVE)
    Loop
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto See
  Melee:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfInTargetInventory("RLTacticalSetBonusActive", 1, "MeleeAOECheck")
    TNT1 A 0 A_CheckFlag("BOSS", "Missile", AAPTR_TARGET)
  MeleeCheckComplete:
    NCYB S 12 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/stomp", 5, 2, 0, 0.6)
    TNT1 A 0 A_Quake(3, 12, 0, 1024, "")
    TNT1 A 0 A_Quake(9, 12, 0, 512, "")
    TNT1 A 0 A_CustomMissile("RLStompSmoke", 10, 20, 0)
    TNT1 A 0 A_JumpIfCloser(72, 1)
    Goto MeleeContinue
    TNT1 A 0 A_CustomMeleeAttack (256 * random(1, 2), "", "", "Melee")
  MeleeContinue:
    TNT1 A 0 A_CustomMissile("RLNightmareCyberStompShockwave",5,0)
    NCYB T 12 A_RadiusThrust(512, 164, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
    NCYB C 12 A_FaceTarget
    Goto See
  MeleeAOECheck:
    TNT1 A 0 A_JumpIfCloser(72, 1)
    Goto Missile
    TNT1 A 0
    Goto MeleeCheckComplete
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfCloser(800, 1)
	Goto ParticleBeam
    TNT1 A 0 A_JumpIfInTargetInventory("RLInquisitorsSetBonusActive", 1, "PlasmaCannon")
    TNT1 A 0 A_Jump(250, "Rockets", "PlasmaCannon")
	Goto ParticleBeam
  Rockets:
    NCYB E 6 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonRockets")
    NCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NCYB E 12 A_FaceTarget
    NCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NCYB E 12 A_FaceTarget
    NCYB F 12 Bright A_CustomMissile("RLCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    Goto See
  SummonRockets:
    NCYB F 12 Bright A_CustomMissile("RLSummonCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NCYB E 12 A_FaceTarget
    NCYB F 12 Bright A_CustomMissile("RLSummonCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NCYB E 12 A_FaceTarget
    NCYB F 12 Bright A_CustomMissile("RLSummonCyberdemonRocket", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    Goto See
  PlasmaCannon:
    NCYB E 6 A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonPlasma")
    NCYB Q 4 Bright A_CustomMissile("RLNightmareCyberdemonPlasmaBall", 50, -20)
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLNightmareCyberdemonPlasmaBall", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLNightmareCyberdemonPlasmaBall", 50, -20, frandom(3,-3), CMF_OFFSETPITCH, frandom(3,-3))
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLNightmareCyberdemonPlasmaBall", 50, -20, frandom(5,-5), CMF_OFFSETPITCH, frandom(5,-5))
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLNightmareCyberdemonPlasmaBall", 50, -20, frandom(7,-7), CMF_OFFSETPITCH, frandom(7,-7))
    NCYB E 12 A_FaceTarget
    Goto See
  SummonPlasma:
    NCYB Q 4 Bright A_CustomMissile("RLSummonNightmareCyberdemonPlasmaBall", 50, -20)
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLSummonNightmareCyberdemonPlasmaBall", 50, -20, frandom(1,-1), CMF_OFFSETPITCH, frandom(1,-1))
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLSummonNightmareCyberdemonPlasmaBall", 50, -20, frandom(3,-3), CMF_OFFSETPITCH, frandom(3,-3))
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLSummonNightmareCyberdemonPlasmaBall", 50, -20, frandom(5,-5), CMF_OFFSETPITCH, frandom(5,-5))
    NCYB E 5 A_FaceTarget
    NCYB Q 4 Bright A_CustomMissile("RLSummonNightmareCyberdemonPlasmaBall", 50, -20, frandom(7,-7), CMF_OFFSETPITCH, frandom(7,-7))
    NCYB E 12 A_FaceTarget
    Goto See
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    TNT1 A 0 A_Chase ("", "ParticleBeam", CHF_DONTMOVE)
    NCYB E 1 A_FaceTarget
    TNT1 A 0 A_Chase ("", "ParticleBeam", CHF_DONTMOVE)
    NCYB E 1 A_FaceTarget
    TNT1 A 0 A_Jump (4, "See", "RageStomp")
    Loop
  ParticleBeam:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfCloser(2048, 1)
	Goto ParticleBeam2
    NCYB E 4 A_FaceTarget
    TNT1 A 0 A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.6, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 8 A_FaceTarget
    NCYB RR 1 Bright A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.6, 0, 0, 1, 0.75, 0, "none", -5)
    TNT1 A 0 A_PlaySound("rlmonsters/particlebeam", CHAN_WEAPON, 2, 0, 0.2)
    TNT1 A 0 A_CustomRailgun (random(40,70), -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 0, "RLParticleBeamPuff", -0.6, 0, 0, 0, 0.2, 1, "RLParticleBeamCore", -5)
    NCYB RRRRRRRRRRRRRR 1 Bright A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.6, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 2 A_FaceTarget
    TNT1 A 0 A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.6, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 10 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "Overwatch")
    Goto ParticleBeam
  ParticleBeam2:
    TNT1 A 0 A_JumpIfCloser(4096, 1)
	Goto ParticleBeam3
    NCYB E 4 A_FaceTarget
    TNT1 A 0 A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.4, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 8 A_FaceTarget
    NCYB RR 1 Bright A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.4, 0, 0, 1, 0.75, 0, "none", -5)
    TNT1 A 0 A_PlaySound("rlmonsters/particlebeam", CHAN_WEAPON, 2, 0, 0.2)
    TNT1 A 0 A_CustomRailgun (random(40,70), -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 0, "RLParticleBeamPuff", -0.4, 0, 0, 0, 0.2, 1, "RLParticleBeamCore", -5)
    NCYB RRRRRRRRRRRRRR 1 Bright A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.4, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 2 A_FaceTarget
    TNT1 A 0 A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.4, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 10 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "Overwatch")
    Goto ParticleBeam
  ParticleBeam3:
    NCYB E 4 A_FaceTarget
    TNT1 A 0 A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.2, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 8 A_FaceTarget
    NCYB RR 1 Bright A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.2, 0, 0, 1, 0.75, 0, "none", -5)
    TNT1 A 0 A_PlaySound("rlmonsters/particlebeam", CHAN_WEAPON, 2, 0, 0.2)
    TNT1 A 0 A_CustomRailgun (random(40,70), -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 0, "RLParticleBeamPuff", -0.2, 0, 0, 0, 0.2, 1, "RLParticleBeamCore", -5)
    NCYB RRRRRRRRRRRRRR 1 Bright A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.2, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 2 A_FaceTarget
    TNT1 A 0 A_CustomRailgun (0, -20, "none", White, RGF_SILENT | RGF_FULLBRIGHT | RGF_EXPLICITANGLE, 1, 50, "RLParticleBeamPuff2", -0.2, 0, 0, 1, 0.75, 0, "none", -5)
    NCYB E 10 A_FaceTarget
    TNT1 A 0 A_MonsterRefire(0, "Overwatch")
    Goto ParticleBeam
  Pain:
    NCYB G 10 A_Pain
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "See")
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    Goto See
  Pain.CyberDisruptor:
    NCYB G 2
    NCYB G 6 A_Pain
    NCYB G 6
    NCYB G 6 A_Pain
    NCYB G 6
    NCYB G 6 A_Pain
    NCYB G 6
    NCYB G 6 A_Pain
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "See")
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    Goto See
  RageStomp:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLCyberdemonToken", 1, "ClearTarget")
    NCYB S 4
    NCYB S 8 A_Pain
    TNT1 A 0 A_PlaySound("rlmonsters/stomp", 5, 2, 0, 0.6)
    TNT1 A 0 A_Quake(3, 12, 0, 1024, "")
    TNT1 A 0 A_Quake(9, 12, 0, 512, "")
    TNT1 A 0 A_CustomMissile("RLStompSmoke", 10, 20, 0)
    TNT1 A 0 A_JumpIfCloser(72, 1)
    Goto RageStompContinue
    TNT1 A 0 A_CustomMeleeAttack (512 * random(1, 2), "", "", "Melee")
  RageStompContinue:
    TNT1 A 0 A_CustomMissile("RLNightmareCyberStompShockwave",5,0)
    NCYB T 12 A_RadiusThrust(512, 164, RTF_NOIMPACTDAMAGE | RTF_NOTMISSILE)
    NCYB C 12
    TNT1 A 0 A_Jump (25, "RageStomp")
    Goto See
  Death:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    NCYB H 10
    TNT1 A 0 A_Quake(7, 40, 0, 1024, "")
    NCYB I 10 Bright A_Scream
    NCYB JKL 10 Bright
    TNT1 A 0 A_Quake(4, 20, 0, 1024, "")
    NCYB M 10 Bright A_NoBlocking
    NCYB NO 10
    NCYB P 30
    NCYB P -1 A_BossDeath
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedLarge",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    NCYB P -1 A_BossDeath
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLNightmareCyberStompShockwave : Rocket
{
   Radius 1
   Height 1
   +MTHRUSPECIES
   +THRUGHOST
   -RANDOMIZE
   -DEHEXPLOSION
   -ROCKETTRAIL
   DamageType "Melee"
   PainType "BluntMelee"
   DeathType "BluntMelee"
   Speed 0
   Damage 0
   SeeSound ""
   DeathSound ""
   Obituary "%o was just another corpse beneath the Nightmare Cyberdemon's hoof."
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_Explode(256, 164, "None", 0, 72)
    TNT1 A 8
    Stop
  }
}

ACTOR RLNightmareCyberdemonPlasmaBall : BFGBall
{
  Radius 13
  Height 8
  Speed 22
  Damage 8
  DamageType "Plasma"
  Scale 0.5
  Projectile
  +RANDOMIZE
  +THRUGHOST
  +SEEKERMISSILE
  +SCREENSEEKER
  +EXPLODEONWATER
  +USEBOUNCESTATE
  Translation "192:207=112:127"
  BounceType "Hexen"
  BounceFactor 1
  WallBounceFactor 1
  BounceCount 2
  RenderStyle Add
  Alpha 0.85
  Decal "BFGLightning"
  SeeSound "rlmonsters/homingplasma"
  BounceSound "rlmonsters/laserhit"
  DeathSound "weapons/plasmax"
  States
  {
  Spawn:
    PPCS CD 4 Bright
    Loop
  Bounce:
    TNT1 AAAA 0 A_SeekerMissile (90, 90, SMF_LOOK | SMF_PRECISE, 256) 
    Goto Spawn
  Death:
    BBB1 ABCDE 4 Bright
    Stop
  }
}

ACTOR RLSummonNightmareCyberdemonPlasmaBall : RLNightmareCyberdemonPlasmaBall
{
  +MTHRUSPECIES
}

ACTOR RLParticleBeamPuff : BulletPuff
{
  -ALLOWPARTICLES
  -RANDOMIZE
  +PUFFONACTORS
  +ALWAYSPUFF
  +PUFFGETSOWNER
  +THRUGHOST
  +MTHRUSPECIES
  VSpeed 0
  Mass 5
  Decal "EnemyBlueBFGLightning"
  Damagetype "Plasma"
  PainType "PlasmaExplosion"
  DeathType "PlasmaExplosion"
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_PlaySound("weapons/bfgx", CHAN_BODY, 1, 0, 0.7)
	Stop
  Melee:
    Goto Spawn
  }
}

ACTOR RLParticleBeamPuff2 : RLParticleBeamPuff
{
  +BLOODLESSIMPACT
  Damagetype "NullDamage"
  States
  {
  Spawn:
    TNT1 A 0
	Stop
  Melee:
    Goto Spawn
  }
}

ACTOR RLParticleBeamPuff3 : RLParticleBeamPuff
{
  +BLOODLESSIMPACT
  Damagetype "Plasma"
  States
  {
  Spawn:
    TNT1 A 0
	Stop
  Melee:
    Goto Spawn
  }
}

ACTOR RLParticleBeamPuff4 : RLParticleBeamPuff
{
  +BLOODLESSIMPACT
  Damagetype "Plasma"
  Paintype "Lightning"
  Deathtype "Lightning"
  States
  {
  Spawn:
    TNT1 A 0
	Stop
  Melee:
    Goto Spawn
  }
}

ACTOR RLParticleBeamCore
{   
   Radius 0
   Height 1
   Speed 0
   PROJECTILE
   RenderStyle ADD
   Alpha 0.95
   Scale 2.1
   States
   {
   Spawn:
      TNT1 A 0
      BBAL A 1 Bright A_SetScale (2.1)
      BBAL B 1 Bright A_SetScale (1.3)
      BBAL C 1 Bright A_SetScale (0.4)
      BBAL DABCDAB 1 Bright
      BBAL CD 1 Bright A_FadeTo (0, 0.35, 1)
      Stop
   }
}

ACTOR RLDRPGSummonedNightmareCyberdemon : RLNightmareCyberdemon
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  }
}

ACTOR RLCCNightmareCyberdemon : RLNightmareCyberdemon
{
  +TOUCHY
  States
  {
  See:
    NCYB DD 4
    NCYB AA 4
    NCYB BB 4
    NCYB CC 4
    Loop
  Melee:
    NCYB S 12
    NCYB T 12
    NCYB C 12
    Goto See
  Missile:
    NCYB E 6
    NCYB F 12 Bright
    NCYB E 12
    NCYB Q 12 Bright
    NCYB E 12
    NCYB R 12 Bright
    Goto See
  Death:
    NCYB H 10
    NCYB I 10 Bright A_Scream
    NCYB JKL 10 Bright
    NCYB M 10 Bright A_NoBlocking
    NCYB NO 10
    NCYB P 30
    NCYB P -1 A_BossDeath
    Stop
  }
}
