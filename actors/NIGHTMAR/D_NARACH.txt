
// NIGHTMARE ARACHNOTRON
//
ACTOR RLNightmareArachnotron : Arachnotron 16528
{
  Health 750
  GibHealth -60
  DamageFactor "Melee", 1.25
  DamageFactor "Crush", 10
  +DONTHARMSPECIES
  +AVOIDMELEE
  SeeSound "nightmarearachnotron/sight"
  ActiveSound "nightmarearachnotron/active"
  PainSound "arachnotron/pain"
  DeathSound "nightmarearachnotron/death"
  CrushPainSound "rlmonsters/crush"
  Species "RLSpidermind"
  Obituary "%o couldn't evade a Nightmare Arachnotron."
  Tag "Nightmare Arachnotron"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLSpiderMindToken", 1)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_TakeInventory ("RLNightmareArachnotronChargeToken", 1)
    NARA ACDF 10 A_Look
    Goto Spawn+6
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto See+4
  See:
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_TakeInventory ("RLNightmareArachnotronChargeToken", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    NARA A 20
    TNT1 A 0 A_PlaySound("nightmarearachnotron/walk", CHAN_BODY)
    NARA AABBCC 3 A_Chase
    TNT1 A 0 A_PlaySound("nightmarearachnotron/walk", CHAN_BODY)
    NARA DDEEFF 3 A_Chase
    Goto See+5
  Missile:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    NARA GGGG 5 A_FaceTarget
    TNT1 A 0 A_Jump (128, "MissileNoDelay")
    TNT1 A 0 A_JumpIfCloser (1024, "MissileNoDelay")
    Goto Charge
  MissileNoDelay:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    NARA H 3 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, frandom(2,-2))
    NARA G 2 A_FaceTarget
    NARA G 1 A_SpidRefire
    Goto MissileNoDelay
  SummonMissile:
    NARA H 3 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, frandom(2,-2))
    NARA G 2 A_FaceTarget
    NARA G 1 A_SpidRefire
    Goto MissileNoDelay
  MissileSpray:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissileSpray")
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, -20)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, -15)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, -10)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, -5)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, 0)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, 5)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, 10)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, 15)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLNightmareArachnotronPlasmaBall", 24, 0, 20)
    NARA GGG 5 A_FaceTarget
    NARA G 1 A_SpidRefire
    Goto MissileNoDelay
  SummonMissileSpray:
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, -20)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, -15)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, -10)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, -5)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, 0)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, 5)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, 10)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, 15)
    NARA G 1 A_FaceTarget
    NARA H 2 Bright A_CustomMissile("RLSummonNightmareArachnotronPlasmaBall", 24, 0, 20)
    NARA GGG 5 A_FaceTarget
    NARA G 1 A_SpidRefire
    Goto MissileNoDelay
  Charge:
    TNT1 A 0 A_PlaySound("rlmonsters/thruster", 6, 1, 0, 0.8)
    TNT1 A 0 A_GiveInventory ("RLNightmareArachnotronChargeToken", 1)
    TNT1 A 0 A_JumpIfCloser (256, "MissileSpray")
    TNT1 A 0 A_PlaySound("rlmonsters/thrusterloop", 7, 1, 1)
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 A_SpawnItemEx ("RLEnemyMeleeWeaponPuff", 16, 32, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_SpawnItemEx ("RLEnemyMeleeWeaponPuff", 16, -32, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    NARA G 2 ThrustThing(angle*256/360, 2, 0, 0)
    NARA G 1 A_SpidRefire
    Goto Charge+2
  Pain:
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLSpiderMindToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfInventory ("RLNightmareArachnotronChargeToken", 1, "Paintrap")
    NARA I 3
    NARA I 3 A_Pain
    Goto See+4
  Pain.CyberDisruptor:
    NARA I 2
    NARA I 6 A_Pain
    NARA I 6
    NARA I 6 A_Pain
    NARA I 6
    NARA I 6 A_Pain
    NARA I 6
    NARA I 6 A_Pain
    Goto See+4
  Paintrap:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    TNT1 A 0 A_TakeInventory ("RLNightmareArachnotronChargeToken", 1)
    TNT1 A 0 A_Pain
    Goto MissileSpray
  Death:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_TakeInventory ("RLNightmareArachnotronChargeToken", 1)
    NARA J 20 A_Scream
    NARA K 7 A_NoBlocking
    NARA LMNO 7
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    NARA P -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    NARA P 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    NARA PONMLKJ 5
    Goto See+1
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_BossDeath
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    NARA P -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLNightmareArachnotronChargeToken : Inventory
{
inventory.maxamount 1
}

ACTOR RLNightmareArachnotronPlasmaBall : ArachnotronPlasma
{
   +THRUGHOST
   Alpha 0.85
   Scale 0.4
   Decal "BFGLightning"
   Damage 8
   DamageType "Plasma"
   SeeSound "rlmonsters/highpowerplasma"
   States
   {
   Spawn:
      PPCS CD 4 Bright
      Loop
   Death:
      BBB2 ABCDE 5 Bright
      Stop
   }
}

ACTOR RLSummonNightmareArachnotronPlasmaBall : RLNightmareArachnotronPlasmaBall
{
   +MTHRUSPECIES
}

ACTOR RLNightmareArachnotronReborn : RLNightmareArachnotron
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    NARA PONMLKJ 5
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_TakeInventory ("RLNightmareArachnotronChargeToken", 1)
    NARA ACDF 10 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedNightmareArachnotron : RLNightmareArachnotron
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCNightmareArachnotron : RLNightmareArachnotron
{
  +TOUCHY
  States
  {
  See:
    NARA A 20
    NARA AABBCC 3
    NARA DDEEFF 3
    Goto See+1
  Missile:
    NARA G 20
    NARA H 3 Bright
    NARA G 3
    Goto Missile+1
  Death:
    NARA J 20 A_Scream
    NARA K 7 A_NoBlocking
    NARA LMNO 7
    NARA P -1 A_BossDeath
    Stop
  }
}
