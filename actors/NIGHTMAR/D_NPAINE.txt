
// NIGHTMARE ELEMENTAL
//
ACTOR RLNightmarePainElemental : PainElemental 16529
{
  Health 760
  GibHealth -100
  MeleeThreshold 148
  MeleeRange 96
  MinMissileChance 18
  DamageFactor "Fire", 0.4
  DamageFactor "Crush", 10
  Species "RLPainElemental"
  +DONTHARMSPECIES
  +QUICKTORETALIATE
  SeeSound "nightmareelemental/sight"
  ActiveSound "nightmareelemental/sight"
  PainSound "nightmareelemental/pain"
  DeathSound "nightmareelemental/death"
  CrushPainSound "rlmonsters/crush"
  Obituary "%o found the Nightmare Elemental did not like %h."
  HitObituary "%o didn't get out of a Nightmare Elemental's way."
  Tag "Nightmare Elemental"
  States
  {
  Spawn:
    NELE A 10 A_Look
    Loop
  See:
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfCloser (128, "MeleeCharge")
    TNT1 A 0 A_CheckSight (2)
    NELE A 3 Fast A_Chase
    Goto See+5
    NELE A 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (128, "MeleeCharge")
    TNT1 A 0 A_CheckSight (2)
    NELE A 3 Fast A_Chase
    Goto See+9
    NELE A 3 A_Wander
    TNT1 A 0 A_Jump (210, 2)
    TNT1 A 0 A_CheckSight ("SoulSpawn")
    TNT1 A 0
    Loop
  MeleeCharge:
    TNT1 A 0 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 5, 0, 0)
    NELE A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    NELE A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    NELE A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    NELE A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    NELE A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_FaceTarget
    NELE A 1 A_Chase ("Melee", "", CHF_DONTMOVE | CHF_NOPLAYACTIVE)
    TNT1 A 0 A_Stop
    Goto See
  Melee:
    TNT1 A 0 ThrustThing(angle*256/360, 5, 0, 0)
    NELE B 3 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 5, 0, 0)
    NELE C 3 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 5, 0, 0)
    NELE D 3 A_FaceTarget
    TNT1 A 0 ThrustThing(angle*256/360, 5, 0, 0)
    NELE C 3 A_CustomMeleeAttack (10 * random(1, 10), "rlmonsters/bite", "", "Melee")
    NELE B 3 A_Chase ("", "")
    Goto See
  Missile:
    NELE BC 5 A_FaceTarget
    NELE D 4 Bright A_FaceTarget
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    NELE D 1 Bright A_CustomMissile("RLNightmareElementalLavaBall", 24)
    NELE CB 3 A_Chase ("", "")
    Goto See
  SummonMissile:
    NELE D 1 Bright A_CustomMissile("RLSummonNightmareElementalLavaBall", 24)
    NELE CB 3 A_Chase ("", "")
    Goto See
  SoulSpawn:
    NELE BC 5
    NELE D 4 Bright
    TNT1 A 0 A_Jump (180, 3)
    NELE D 1 Bright A_PainAttack ("RLNightmareLostSoul", 0, PAF_NOSKULLATTACK | PAF_AIMFACING)
    TNT1 A 0 A_Jump (256, 2)
    NELE D 1 Bright A_PainAttack ("RLLostSoul", 0, PAF_NOSKULLATTACK | PAF_AIMFACING)
    NELE CB 3 A_Chase ("", "")
    Goto See
  Pain:
    TNT1 A 0 A_Stop
    NELE E 6
    NELE E 6 A_Pain
    Goto See
  Death:
    NELE E 8
    NELE G 8 A_Scream
    NELE HI 8 Bright
    TNT1 A 0 A_Playsound("rlmonsters/fireblast", 6)
    NELE J 8 Bright A_CustomMissile("RLNightmareElementalExplosion",12,0)
    NELE K 8 Bright
    Stop
  Raise:
    Stop
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 1)
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR RLNightmareElementalExplosion : Rocket
{
   Radius 1
   Height 1
   +THRUGHOST
   +MTHRUSPECIES
   -RANDOMIZE
   -DEHEXPLOSION
   -ROCKETTRAIL
   DamageType "Fire"
   PainType "Explosion"
   DeathType "Explosion"
   Speed 0
   Damage 0
   SeeSound ""
   DeathSound ""
   Obituary "%o stood too close to the Nightmare Elemental's blast."
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_Explode (128, 128)
    TNT1 A 8
    Stop
  }
}

ACTOR RLNightmareElementalLavaBall
{
   Radius 8
   Height 8
   Speed 15
   Damage 6
   DamageType Fire
   PainType "Explosion"
   DeathType "Explosion"
   Scale 1.0
   BounceType "Hexen"
   BounceFactor 1
   WallBounceFactor 1
   BounceCount 3
   ReactionTime 500
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.95
   +THRUGHOST
   +EXPLODEONWATER
   SeeSound "nightmareelemental/attack"
   BounceSound "nightmareelemental/bounce"
   DeathSound "rlmonsters/fireblast"
   States
   {
   Spawn: 
      TNT1 A 0 A_Countdown
      BAL3 AB 4 Bright A_SpawnItem("RLNightmareElementalLavaBallTrail",0,0)
      loop
   Death:
      BAL3 C 5 Bright A_Explode (16, 96)
      BAL3 DE 5 Bright
      stop
   }
}

ACTOR RLSummonNightmareElementalLavaBall : RLNightmareElementalLavaBall
{
   +MTHRUSPECIES
}

ACTOR RLNightmareElementalLavaBallTrail
{   
   Radius 2
   Height 2
   Speed 0
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.85
   +NOINTERACTION
   States
   {
   Spawn:
      TNT1 A 3
      RPUF ABCDE 3 Bright
      Stop 
   }
}

ACTOR RLDRPGSummonedNightmarePainElemental : RLNightmarePainElemental
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCNightmarePainElemental : RLNightmarePainElemental
{
  +TOUCHY
  States
  {
  See:
    NELE AA 3
    Loop
  Melee:
    NELE BCDCB 3
    Goto See
  Missile:
    NELE BC 5
    NELE D 4 Bright
    NELE D 1 Bright
    NELE CB 3
    Goto See
  Death:
    NELE E 8
    NELE G 8 A_Scream
    NELE HI 8 Bright
    NELE J 8 Bright
    NELE K 8 Bright
    Stop
  }
}
