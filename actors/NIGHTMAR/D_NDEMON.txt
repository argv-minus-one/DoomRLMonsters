
// NIGHTMARE DEMON
//
ACTOR RLNightmareDemon : Demon 16522
{
  GibHealth -100
  Health 300
  DamageFactor "Crush", 10
  SeeSound "nightmaredemon/sight"
  ActiveSound "nightmaredemon/active"
  PainSound "nightmaredemon/pain"
  DeathSound "nightmaredemon/death"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Species "RLDemon"
  Obituary "%o was distorted by a Nightmare Demon."
  Tag "Nightmare Demon"
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory ("RLMonsterInitializerToken", 1, 3)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("RLDemonToken", 1)
    NDEM AB random(9,10) A_Look
    Goto Spawn+3
  ClearTarget:
    TNT1 A 0 A_ClearTarget
    TNT1 A 0 A_LookEx (LOF_NOSEESOUND, 0, 0, 0, 360, "See")
    Goto Spawn
  See:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfCloser (192, "ChaseA")
    NDEM AA 2 Fast A_Chase
    Goto SeeB
  SeeB:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfCloser (192, "ChaseB")
    NDEM BB 2 Fast A_Chase
    Goto SeeC
  SeeC:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfCloser (192, "ChaseC")
    NDEM CC 2 Fast A_Chase
    Goto SeeD
  SeeD:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfCloser (192, "ChaseD")
    NDEM DD 2 Fast A_Chase
    Goto See
  ChaseA:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    NDEM AA 1 A_Chase
    TNT1 A 0 A_JumpIfCloser (192, "ChaseB")
    Goto SeeB
  ChaseB:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    NDEM BB 1 A_Chase
    TNT1 A 0 A_JumpIfCloser (192, "ChaseC")
    Goto SeeC
  ChaseC:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    NDEM CC 1 A_Chase
    TNT1 A 0 A_JumpIfCloser (192, "ChaseD")
    Goto SeeD
  ChaseD:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    NDEM DD 1 A_Chase
    TNT1 A 0 A_JumpIfCloser (192, "ChaseA")
    Goto See
  TeleportMelee:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareDemonTeleportEffect2", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	Goto Melee
  Melee:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_PlaySound("nightmaredemon/attack", CHAN_VOICE)
    NDEM EF 8 Fast A_FaceTarget
    NDEM G 8 Fast A_CustomMeleeAttack (5 * random(1, 10), "", "", "Melee")
    Goto See
  Pain:
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 2)
    TNT1 A 0 A_JumpIfInTargetInventory ("RLDemonToken", 1, "ClearTarget")
    TNT1 A 0 A_JumpIfInventory ("RLNightmareDemonTeleportToken", 3, 2)
    TNT1 A 0 A_JumpIfCloser (192, "TeleportPain")
    NDEM H 1
    TNT1 A 0 A_TakeInventory ("RLNightmareDemonTeleportToken", 3)
    NDEM H 2 Fast A_Pain
    Goto See
  TeleportPain:
    TNT1 A 0 A_GiveInventory ("RLNightmareDemonTeleportToken", 1)
    TNT1 A 0 A_SpawnItemEx ("RLNightmareDemonTeleportEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_Pain
    TNT1 AAAAA 0 A_Chase ("TeleportMelee", "", CHF_NOPLAYACTIVE)
    TNT1 A 0 A_SpawnItemEx ("RLNightmareDemonTeleportEffect3", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto See
  Death:
    NDEM I 8
    NDEM J 8 A_Scream
    NDEM K 4
    NDEM L 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    NDEM M 4
    NDEM N -1
    Stop
  XDeath:
    NDEM O 4
    NDEM P 4 A_XScream
    NDEM Q 4
    NDEM RS 4 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    NDEM TUV 4
    NDEM W -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    NDEM N 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    NDEM NMLKJI 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Goto RaiseContinue
  Crush:
    TNT1 A 1 A_SpawnItem("RLCrushedMedium",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    NDEM N -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLNightmareDemonTeleportToken : Inventory
{
inventory.maxamount 3
}

ACTOR RLNightmareDemonTeleportEffect1
{
  Radius 30
  Height 56
  +NOGRAVITY
  +FLOORCLIP
  +NOLIFTDROP
  +NOTONAUTOMAP
  Alpha 0.8
  RenderStyle Subtract
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_SetScale (ScaleX+0.01)
    NDEM H 1 A_FadeTo (0, 0.06, 1)
    Loop
  }
}

ACTOR RLNightmareDemonTeleportEffect2 : RLNightmareDemonTeleportEffect1
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_SetScale (ScaleX+0.01)
    NDEM E 1 A_FadeTo (0, 0.06, 1)
    Loop
  }
}

ACTOR RLNightmareDemonTeleportEffect3 : RLNightmareDemonTeleportEffect1
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_SetScale (ScaleX+0.01)
    NDEM A 1 A_FadeTo (0, 0.06, 1)
    Loop
  }
}

ACTOR RLNightmareDemonReborn : RLNightmareDemon
{
  +LOOKALLAROUND
  +NOPAIN
  States
  {
  Spawn:
    TNT1 A 0 NoDelay A_JumpIfInventory("RLResurrectionComplete", 1, "SpawnNormal")
    NDEM NMLKJI 5
    TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
    TNT1 A 0 A_GiveInventory("RLResurrectionComplete", 1)
  SpawnNormal:
    NDEM AB 10 A_Look
    Loop
  }
}

ACTOR RLDRPGSummonedNightmareDemon : RLNightmareDemon
{
  -COUNTKILL
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCNightmareDemon : RLNightmareDemon
{
  +TOUCHY
  States
  {
  Spawn:
    NDEM AB 10
    Loop
  See:
    NDEM AABBCCDD 2
    Loop
  Melee:
    NDEM EF 8
    NDEM G 8
    Goto See
  Death:
    NDEM I 8
    NDEM J 8 A_Scream
    NDEM K 4
    NDEM L 4 A_NoBlocking
    NDEM M 4
    NDEM N -1
    Stop
  }
}
