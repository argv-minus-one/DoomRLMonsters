
// ELITE FORMER HUMAN COMMANDO
//
ACTOR RLEliteCommandoMinigun : Chaingunguy 16519
{
  Health 150
  DamageFactor "Crush", 10
  DamageFactor "Plasma", 0.8
  SeeSound "eliteformer/sight"
  PainSound "eliteformer/pain"
  DeathSound "eliteformer/death"
  ActiveSound "eliteformer/active"
  CrushPainSound "rlmonsters/crush"
  AttackSound ""
  Obituary "%o was torn asunder by the hailstorm from an Elite Former Human Commando."
  DropItem "RLClipBox"
  DropItem "RLMinigunPickup" 30
  +AVOIDMELEE
  Tag "Elite Human Commando"
  States
  {
  Spawn:
    TNT1 A 0 A_StopSound(7)
    ECMI AB 10 A_Look
    Loop
  See:
    TNT1 A 0 A_StopSound(7)
    TNT1 A 0 A_JumpIfInventory ("RLEliteCommandoMinigunShotCounter", 200, "RetreatReload")
    ECMI AABBCCDD 4 A_Chase
    Loop
  Overwatch:
    TNT1 A 0 A_JumpIfInventory ("RLEliteCommandoMinigunShotCounter", 200, "RetreatReload")
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunspin", 7, 1, 1, 1.2)
    ECMI U 0 A_Chase ("", "MissileRestartFire", CHF_DONTMOVE)
    ECMI U 4 A_FaceTarget
    ECMI U 0 A_Chase ("", "MissileRestartFire", CHF_DONTMOVE)
    ECMI U 4 A_FaceTarget
    TNT1 A 0 A_Jump (8, "MissileEnd")
    Loop
  RetreatReload:
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 1)
    ECMI AA 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ECMI BB 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ECMI CC 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    ECMI DD 3 A_Wander
    TNT1 A 0 A_JumpIfCloser (180, 2)
    TNT1 A 0 A_Jump (24, "Reload")
    TNT1 A 0
    Goto RetreatReload+1
  MissileRestartFire:
    ECMI U 4 A_FaceTarget
    TNT1 A 0 A_Jump (256, 1,2,3,4,5,6)
    ECMI UUUUUU 4 A_FaceTarget
    Goto MissileFire
  Missile:
    ECMI U 8 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunwindup", 7)
    ECMI UUUUUU 4 A_FaceTarget
  MissileFire:
    TNT1 A 0 A_JumpIfInventory ("RLEliteCommandoMinigunShotCounter", 200, "MissileEnd")
    TNT1 A 0 A_GiveInventory ("RLEliteCommandoMinigunShotCounter", 1)
    TNT1 A 0 A_PlaySound("rlmonsters/minigun", CHAN_WEAPON)
    TNT1 A 0 A_JumpIfInventory ("RLEliteCommandoMinigunStateCounter", 1, "MissileAlt")
    TNT1 A 0 A_GiveInventory ("RLEliteCommandoMinigunStateCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissile")
    ECMI F 1 Bright A_CustomBulletAttack (30, 5, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ECMI U 0 A_MonsterRefire(2, "Overwatch")
    Goto MissileFire
  SummonMissile:
    ECMI F 1 Bright A_CustomBulletAttack (6, 2, 1, random(1,3) * 5, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ECMI U 0 A_MonsterRefire(2, "Overwatch")
    Goto MissileFire
  MissileAlt:
    TNT1 A 0 A_TakeInventory ("RLEliteCommandoMinigunStateCounter", 1)
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, "SummonMissileAlt")
    ECMI E 1 Bright A_CustomBulletAttack (30, 5, 1, random(1,5) * 3, "RLEnemyBulletPuff", 0, CBAF_NORANDOM)
    ECMI U 0 A_MonsterRefire(2, "Overwatch")
    Goto MissileFire
  SummonMissileAlt:
    ECMI E 1 Bright A_CustomBulletAttack (6, 2, 1, random(1,3) * 5, "RLEnemySummonBulletPuff", 0, CBAF_NORANDOM)
    ECMI U 0 A_MonsterRefire(2, "Overwatch")
    Goto MissileFire
  MissileEnd:
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunwinddown", 7)
    ECMI UUUUU 4
    ECMI U 2
    Goto See
  Reload:
    ECMI UUUUUU 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/pistolreloadout", 7)
    ECMI UU 5 A_FaceTarget
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunload", 7)
    TNT1 A 0 A_TakeInventory ("RLEliteCommandoMinigunShotCounter", 200)
    ECMI UUUUUUUU 4 A_FaceTarget
    ECMI U 2 A_FaceTarget
    TNT1 A 0 A_PlaySound("rlmonsters/chaingunwinddown", 7)
    ECMI UUUUU 4 A_FaceTarget
    ECMI U 2 A_FaceTarget
    Goto See
  Pain:
    TNT1 A 0 A_StopSound(7)
    ECMI G 3
    ECMI G 3 A_Pain
    Goto See
  Death:
    TNT1 A 0 A_StopSound(7)
    ECMI H 5
    ECMI I 5 A_Scream
    ECMI J 5 A_NoBlocking
    ECMI K 5
    TNT1 A 0 A_SpawnItem ("RLBodyThud", 0, 0, 0, 0)
    ECMI L 5
    ECMI M -1
    Stop
  XDeath:
    TNT1 A 0 A_GiveToTarget ("RLBerserkerBloodlustBonusCheck", 1)
    TNT1 A 0 A_StopSound(7)
    ECMI N 5
    ECMI O 5 A_XScream
    ECMI P 5 A_NoBlocking
    TNT1 A 0 A_SpawnItem ("RLGibThud", 0, 0, 0, 0)
    ECMI Q 5
    ECMI R 5
    ECMI S -1
    Stop
  Raise:
    TNT1 A 0 A_ChangeFlag ("NOBLOCKMAP", 0)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
    ECMI M 2
    TNT1 A 0 A_JumpIfInventory ("RLNightmareResurrectionToken", 1, "NightmareRaise")
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, "CyberneticRaise")
    TNT1 A 0 A_SpawnItemEx ("RLResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
  RaiseContinue:
    TNT1 A 0 A_JumpIfInventory ("RLCyberneticResurrectionToken", 1, 2)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
    TNT1 A 0 A_ChangeFlag ("FRIGHTENED", 0)
    TNT1 A 0 A_TakeInventory ("RLEliteCommandoMinigunShotCounter", 200)
    ECMI M 5
    ECMI L 5
    ECMI K 5
    ECMI J 5
    ECMI I 5
    ECMI H 5
    Goto See
  NightmareRaise:
    TNT1 A 0 A_SpawnItemEx ("RLNightmareResurrectionFire", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION) 
    TNT1 A 0 A_ChangeFlag ("ALWAYSFAST", 1)
    Goto RaiseContinue
  CyberneticRaise:
    TNT1 A 0 A_SpawnItemEx ("RLCyberneticResurrectionSparks", 0, 0, 20, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    TNT1 A 0 A_ChangeFlag ("NOPAIN", 1) 
    TNT1 A 0 A_ChangeFlag ("SEEINVISIBLE", 1)
    TNT1 A 0 A_ChangeFlag ("MISSILEMORE", 1)
    Goto RaiseContinue
  Crush:
    TNT1 A 0 A_StopSound(7)
    TNT1 A 1 A_SpawnItem("RLCrushedSmall",0,0,0,0)
    Stop
  Death.Massacre:
    TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_Stop
    TNT1 A 0 A_JumpIfInventory ("DRPGDRLASummonedMonsterToken", 1, 3)
    TNT1 A 0 ThrustThingZ (0, 20, 0, 0)
    ECMI M -1
    Stop
    TNT1 A 0 A_SpawnItemEx ("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

Actor RLEliteCommandoMinigunShotCounter : Inventory
{
inventory.maxamount 200
}

Actor RLEliteCommandoMinigunStateCounter : Inventory
{
inventory.maxamount 1
}

ACTOR RLDRPGSummonedEliteCommando : RLEliteCommandoMinigun
{
  -COUNTKILL
  +AVOIDMELEE
  +NOBLOCKMONST
  +NOTELESTOMP
  +NOFEAR
  +SEEINVISIBLE
  +DONTDRAIN
  +ALWAYSFAST
  +QUICKTORETALIATE
  +MISSILEMORE
  +THRUSPECIES
  Species "Player"
  States
  {
  Spawn:
	TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::Spawn
	See:
	TNT1 A 0 ACS_NamedExecuteAlways("MonsterInit", 0)
    TNT1 A 0 A_GiveInventory ("RLMonsterInitializerToken", 1)
    TNT1 A 0 A_GiveInventory ("DRPGDRLASummonedMonsterToken", 1)
	Goto Super::See
  Raise:
    Stop
  }
}

ACTOR RLCCEliteCommandoMinigun : RLEliteCommandoMinigun
{
  +TOUCHY
  States
  {
  Spawn:
    ECMI AB 10
    Loop
  See:
    ECMI UUUUUUUU 4
    Loop
  Missile:
    ECMI FE 1 Bright
    ECMI E 1 Bright
    Loop
  Death:
    ECMI H 5
    ECMI I 5 A_Scream
    ECMI J 5 A_NoBlocking
    ECMI K 5
    ECMI L 5
    ECMI M -1
    Stop
  }
}
